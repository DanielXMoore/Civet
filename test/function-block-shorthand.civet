{ testCase, throws } from ./helper.civet

describe "&. function block shorthand", ->
  testCase """
    accessor
    ---
    x.map &.name
    ---
    x.map($ => $.name)
  """

  testCase """
    optional access
    ---
    x.map &?.name
    ---
    x.map($ => $?.name)
  """

  testCase """
    omit &
    ---
    x.map .name
    ---
    x.map($ => $.name)
  """

  testCase """
    glob accessor
    ---
    x.map &.{name,age}
    ---
    x.map($ => ({name:$.name,age:$.age}))
  """

  testCase """
    glob accessor without &
    ---
    x.map .{name,age}
    ---
    x.map($ => ({name:$.name,age:$.age}))
  """

  // #1234
  testCase """
    omit & with private identifier
    ---
    class Foo
      #name: string
      copy(foo: Foo)
        foo ||> .#name = #name
    ---
    class Foo {
      #name: string
      copy(foo: Foo) {
        foo.#name = this.#name;return foo
      }
    }
  """

  testCase """
    identity function
    ---
    x.map &
    ---
    x.map($ => $)
  """

  testCase """
    identity with content on next line
    ---
    x.filter &
    y
    ---
    x.filter($ => $)
    y
  """

  testCase """
    omit & with ?
    ---
    x.map ?.name
    ---
    x.map($ => $?.name)
  """

  testCase """
    omit & doesn't break numbers
    ---
    x.map .1
    ---
    x.map(.1)
  """

  testCase """
    index
    ---
    x.map &[0]
    ---
    x.map($ => $[0])
  """

  testCase """
    optional index
    ---
    x.map &?[0]
    ---
    x.map($ => $?.[0])
  """

  testCase """
    index with expression
    ---
    x.map &[b + 1]
    ---
    x.map($ => $[b + 1])
  """

  testCase """
    slice
    ---
    x.map &[0..2]
    ---
    x.map($ => $.slice(0, 2 + 1 || 1/0))
  """

  testCase """
    reverse slice
    ---
    x.map &[..>=]
    ---
    type RSliceable<R> = string | {length: number; slice(start: number, end: number): {reverse(): R}}
    var rslice: <R, T extends string | RSliceable<R>>(a: T, start?: number, end?: number) => T extends string ? string : T extends RSliceable<infer R> ? R : never = ((a, start = -1, end = -1) => {
      const l = a.length
      if (start < 0) start += l
      if (++end < 0) end += l
      if (typeof a === 'string') {
        let r = ""
        if (start > l-1) start = l-1
        if (end < 0) end = 0
        for (let i = start; i >= end; --i) r += a[i]
        return r as any
      } else {
        return a.slice(end, start + 1).reverse()
      }
    });
    x.map($ => rslice($, -1, 0 - 1))
  """

  testCase """
    modulo access
    ---
    'abc'[&%]
    ---
    var modulo = ((a: number, b: number) => (a % b + b) % b) as ((a: number, b: number) => number) & ((a: bigint, b: bigint) => bigint);
    $ => 'abc'[modulo($, 'abc'.length)]
  """

  testCase """
    kitchen sink
    ---
    x.map &.profile?.name?[0..2]
    ---
    x.map($ => $.profile?.name?.slice(0, 2 + 1 || 1/0))
  """

  testCase """
    assignment
    ---
    x.map .name = "foo"
    ---
    x.map($ => $.name = "foo")
  """

  testCase """
    multiple assignment
    ---
    x.map .name = name += "foo"
    ---
    x.map($ => $.name = name += "foo")
  """

  throws """
    placeholder assignment
    ---
    & = "hello"
    ---
    ParseErrors: unknown:1:6 Lone placeholder (&) is not a valid left-hand side
  """

  testCase """
    addition
    ---
    x.map &+1
    x.map & + 1
    x.map 1+&
    x.map 1 + &
    ---
    x.map($ => $+1)
    x.map($1 => $1 + 1)
    x.map($2 => 1+$2)
    x.map($3 => 1 + $3)
  """

  testCase """
    addition in parens
    ---
    (&+1)
    ---
    $ => ($+1)
  """

  testCase """
    subtraction
    ---
    x.map &-1
    ---
    x.map($ => $-1)
  """

  testCase """
    bitwise and
    ---
    x.map & & 1
    x.map & &
      1
    x.map 1 & &
    x.map 1 &
      &
    x.map 1 & &.x
    ---
    x.map($ => $ & 1)
    x.map($1 => $1 &
      1)
    x.map($2 => 1 & $2)
    x.map($3 => 1 &
      $3)
    x.map($4 => 1 & $4.x)
  """

  testCase """
    bitwise or
    ---
    x.map & | 1
    ---
    x.map($ => $ | 1)
  """

  testCase """
    logical and
    ---
    x.map & && 1
    ---
    x.map($ => $ && 1)
  """

  testCase """
    logical or
    ---
    x.map & || 1
    ---
    x.map($ => $ || 1)
  """

  testCase """
    increment modulo
    ---
    x.map (& + 1) % n
    ---
    x.map($ => ($ + 1) % n)
  """

  testCase """
    chained comparisons
    ---
    x.map & > a < b
    x.map a < & < b
    ---
    x.map($ => $ > a && a < b)
    x.map($1 => a < $1 && $1 < b)
  """

  testCase """
    access with chained comparisons
    ---
    x.map &.foo > a < b
    ---
    x.map($ => $.foo > a && a < b)
  """

  testCase """
    special operator
    ---
    x.map & is in a
    ---
    x.map($ => a.includes($))
  """

  testCase """
    access with special operator
    ---
    x.map &.foo is in a
    ---
    x.map($ => a.includes($.foo))
  """

  testCase """
    assignment and binary operator
    ---
    x.map &.name = 'name' + i++
    ---
    x.map($ => $.name = 'name' + i++)
  """

  testCase """
    $ as local value
    ---
    x.map & < 3 && $ > 1
    ---
    x.map($1 => $1 < 3 && $ > 1)
  """

  testCase """
    unary !
    ---
    x.map !&
    ---
    x.map($ => !$)
  """

  testCase """
    unary !!
    ---
    x.map !!&
    ---
    x.map($ => !!$)
  """

  testCase """
    unary not
    ---
    x.map not &
    ---
    x.map($ => !$)
  """

  testCase """
    unary not not
    ---
    x.map not not &
    ---
    x.map($ => !!$)
  """

  testCase """
    unary ~
    ---
    x.map ~&
    ---
    x.map($ => ~$)
  """

  testCase """
    unary -
    ---
    x.map -&
    ---
    x.map($ => -$)
  """

  testCase """
    unary +
    ---
    x.map +&
    ---
    x.map($ => +$)
  """

  testCase """
    unary typeof
    ---
    x.map typeof &
    ---
    x.map($ => typeof $)
  """

  testCase """
    unary delete
    ---
    x.forEach delete .old
    ---
    x.forEach($ => delete $.old)
  """

  testCase """
    unary await
    ---
    await.all x.map await .json()
    ---
    await Promise.all(x.map(async $ => await $.json()))
  """

  throws """
    not.prop
    ---
    x.map not.prop
    ---
    ParseError
  """

  throws """
    typeof.prop
    ---
    x.map typeof.prop
    ---
    ParseError
  """

  throws """
    delete.prop
    ---
    x.map delete.prop
    ---
    ParseError
  """

  testCase """
    call expression
    ---
    x.map &.foo()
    ---
    x.map($ => $.foo())
  """

  testCase """
    call expression with arguments
    ---
    x.map &.foo(1, 2, 3)
    ---
    x.map($ => $.foo(1, 2, 3))
  """

  testCase """
    call expression with arguments and splat
    ---
    x.map &.foo(b, ...rest)
    ---
    x.map($ => $.foo(b, ...rest))
  """

  testCase """
    call expression with implicit application
    ---
    x.map &.foo 1, 2
    ---
    x.map($ => $.foo(1, 2))
  """

  testCase """
    access, index, and call expression followed by index and safe access
    ---
    x.map &.foo[0].bar()[2]?.baz
    ---
    x.map($ => $.foo[0].bar()[2]?.baz)
  """

  testCase """
    template literal
    ---
    x.map &`foo`
    ---
    x.map($ => $`foo`)
  """

  testCase """
    array wrapping
    ---
    x.map [&]
    x.map [&, x]
    x.map [&, &]
    x.map [...&]
    x.map [[&]]
    ---
    x.map($ => [$])
    x.map($1 => [$1, x])
    x.map($2 => [$2, $2])
    x.map($3 => [...$3])
    x.map($4 => [[$4]])
  """

  testCase """
    object wrapping
    ---
    x.map x: &
    x.map {x: &}
    ---
    x.map($ => ({x: $}))
    x.map($1 => ({x: $1}))
  """

  testCase """
    object expression wrapping
    ---
    x.map {u: 'up', d: 'down'}[&]
    x.map {x: &} + 'end'
    ---
    x.map($ => ({u: 'up', d: 'down'}[$]))
    x.map($1 => ({x: $1} + 'end'))
  """

  testCase """
    ternary expression
    ---
    a := & ? & : 0
    b := & ? &+1 : -&
    ---
    const a = $ => $ ? $ : 0
    const b = $1 => $1 ? $1+1 : -$1
  """

  testCase """
    if expression
    ---
    check := if & then x else y
    ---
    const check = $ => ($? x : y)
  """

  testCase """
    if expression with & in then
    ---
    check := if & then &+1 else y
    ---
    const check = $ => ($? ($+1) : y)
  """

  testCase """
    if expression with & in else
    ---
    check := if & then x else &
    ---
    const check = $ => ($? x : $)
  """

  testCase """
    big if/else expression
    ---
    arr.map
      if &.x
        &.foo
      else if &.y
        &.bar
      else
        &.baz
    ---
    arr.map($ =>\u0020
      (()=>{if ($.x) {
        return $.foo
      }
      else if ($.y) {
        return $.bar
      }
      else {
        return $.baz
      }})())
  """

  testCase """
    if expression without & in condition
    ---
    roundUp := if even then & else &+1
    ---
    let ref;if (even) ref = $ => $; else ref = $1 => $1+1;const roundUp =ref
  """

  testCase """
    if statement
    ---
    =>
      if & then x else y
    ---
    () => {
      return $ => { if ($) return x; else return y}
    }
  """

  testCase """
    it works with assignment
    ---
    x = &.name
    x = .name
    ---
    x = $ => $.name
    x = $1 => $1.name
  """

  testCase """
    it works with assignment and binary operators
    ---
    x = & + 1
    ---
    x = $ => $ + 1
  """

  testCase """
    it works with const assignment
    ---
    const x = &.foo
    ---
    const x = $ => $.foo
  """

  testCase """
    it works with const assignment shorthand
    ---
    x := &.foo
    ---
    const x = $ => $.foo
  """

  testCase """
    it works with return
    ---
    return &.foo
    ---
    return $ => $.foo
  """

  testCase """
    it works with yield
    ---
    -> yield &.foo
    ---
    (function*() { yield $ => $.foo })
  """

  testCase """
    await
    ---
    x.map & + await f()
    ---
    x.map(async $ => $ + await f())
  """

  testCase """
    non-null
    ---
    arr.filter &?
    ---
    arr.filter($ => ($ != null))
  """

  testCase """
    type cast
    ---
    arr.map & as any
    ---
    arr.map($ => $ as any)
  """

  testCase """
    null
    ---
    arr.filter !&?
    ---
    arr.filter($ => ($ == null))
  """

  testCase """
    property non-null
    ---
    hasNull := arr.every .x?
    ---
    const hasNull = arr.every($ => ($.x != null))
  """

  testCase """
    property null
    ---
    hasNull := arr.every !.x?
    ---
    const hasNull = arr.every($ => ($.x == null))
  """

  testCase """
    & function block followed by pipe
    ---
    &.foo |> bar
    ---
    $ => bar($.foo)
  """

  testCase """
    mix with . at different level
    ---
    f . + &
    ---
    $ => f($1 => $ + $1)
  """

  testCase """
    mix with . at similar level
    ---
    & + f .
    ---
    $ => $ + ($1 => f($1))
  """

  testCase """
    autoVar
    ---
    'civet autoVar'
    & + x = 5
    ---
    $ => { var x; return $ + (x = 5)}
  """

  testCase """
    autoLet
    ---
    'civet autoLet'
    & + x = 5
    ---
    let x;
    $ => $ + (x = 5)
  """

  testCase """
    for..of
    ---
    for x of &
    for x of &
      x*x
    ---
    $ => { const results=[];for (const x of $) {results.push(x)};return results;};
    $1 => { const results1=[];for (const x of $1) {
      results1.push(x*x)
    };return results1;}
  """

  throws """
    assignment position in for..of
    ---
    for & of &
    ---
    ParseErrors: unknown:1:6 Lone placeholder (&) is not a valid left-hand side
  """

  testCase """
    for each
    ---
    for each bit of &
      Number bit
    ---
    $ => { const results=[];for (let i = 0, len = $.length; i < len; i++) {const bit = $[i];
      results.push(Number(bit))
    };return results;}
  """

  describe "multiple &", ->
    testCase """
      self-addition
      ---
      x.map &+&
      x.map & + &
      ---
      x.map($ => $+$)
      x.map($1 => $1 + $1)
    """

    testCase """
      self-multiplication
      ---
      &*&
      ---
      $ => $*$
    """

    testCase """
      &.x + &.y
      ---
      &.x + &.y
      ---
      $ => $.x + $.y
    """

    testCase """
      .x + .y
      ---
      .x + .y
      ---
      $ => $.x + $.y
    """

    testCase """
      in function call
      ---
      f &.x + &.y
      f .x + .y
      ---
      f($ => $.x + $.y)
      f($1 => $1.x + $1.y)
    """

    testCase """
      in one-line fat arrow function
      ---
      => &.x + &.y
      ---
      () => $ => $.x + $.y
    """

    testCase """
      in one-line thin arrow function
      ---
      -> &.x + &.y
      ---
      (function() { return $ => $.x + $.y })
    """

    testCase """
      in (or)
      ---
      (or)
        &.x
        &.y
      ---
      $ => ((
        $.x)||(
        $.y))
    """

  describe "typed &", ->
    testCase """
      identity
      ---
      &: number
      ---
      ($: number) => $
    """

    testCase """
      optional
      ---
      &?: number
      ---
      ($?: number) => $
    """

    testCase """
      with operator
      ---
      &: number + 1
      ---
      ($: number) => $ + 1
    """

    testCase """
      with pipe
      ---
      &: number |> foo
      ---
      ($: number) => foo($)
    """

    testCase """
      long type needs parens
      ---
      &: number | string
      &: (number | string)
      &: number < 0 ? -1 : 1
      ---
      ($: number) => $ | string;
      ($1: (number | string)) => $1;
      ($2: number) => $2 < 0 ? -1 : 1
    """

describe "(op) shorthand", ->
  testCase """
    binary op
    ---
    items.reduce (+), 0
    items.reduce (&&), true
    items.reduce (||), false
    ---
    items.reduce(((a,b) => a+b), 0)
    items.reduce(((a1,b1) => a1&&b1), true)
    items.reduce(((a2,b2) => a2||b2), false)
  """

  testCase """
    binary op with spaces
    ---
    ( +/*rhs*/)
    (in)
    ( in)
    (in )
    (  in  )
    (not in)
    (!in)
    (instanceof)
    (!instanceof)
    (<?)
    (!<?)
    ---
    ((a,b) => a +/*rhs*/b);
    ((a1,b1) => a1 in b1);
    ((a2,b2) => a2 in b2);
    ((a3,b3) => a3 in b3);
    ((a4,b4) => a4  in  b4);
    ((a5,b5) => !(a5 in b5));
    ((a6,b6) => !(a6 in b6));
    ((a7,b7) => a7 instanceof b7);
    ((a8,b8) => !(a8 instanceof b8));
    ((a9,b9) => a9 instanceof b9);
    ((a10,b10) => !(a10 instanceof b10))
  """

  testCase """
    binary &
    ---
    items.reduce (&), 0xffff
    ---
    items.reduce(((a,b) => a&b), 0xffff)
  """

  testCase """
    compose binary op
    ---
    compose (+), (*)
    ---
    compose(((a,b) => a+b), ((a1,b1) => a1*b1))
  """

  testCase """
    apply binary op
    ---
    (+) 1, 2
    ---
    ((1)+( 2))
  """

  testCase """
    apply binary op with one argument
    ---
    (+) 1
    ---
    (1)
  """

  testCase """
    apply binary op with one argument in pipeline
    ---
    foo |> (+) x
    ---
    (x)(foo)
  """

  testCase """
    apply binary op with no arguments
    ---
    (+)()
    (+)( /* comment */ )
    ---
    ((a,b) => a+b)();
    ((a1,b1) => a1+b1)( /* comment */ )
  """

  testCase """
    apply binary op ASI
    ---
    (+) 1,2
    (+) 1
    (+) 1,2
    ---
    ((1)+(2));
    (1);
    ((1)+(2))
  """

  testCase """
    apply binary op with many complex arguments
    ---
    (*) x+y, z+w, a**b
    ---
    ((x+y)*( z+w)*( a**b))
  """

  testCase """
    indented apply binary op
    ---
    (and)
      x
      y, yy
      z
    ---
    ((
      x)&&(
      y)&&( yy)&&(
      z))
  """

  testCase """
    apply binary op with trailing comma
    ---
    (+)(1, )
    (+)(1, 2, )
    (+)(1, 2, /* comment */ )
    ---
    (1 );
    ((1)+( 2 ));
    ((1)+( 2 /* comment */ ))
  """

  testCase """
    apply binary op with comma operator
    ---
    (+)((1, 2), (3, 4))
    ---
    (((1, 2))+( (3, 4)))
  """

  testCase """
    apply binary op with function call
    ---
    (+)
      f(1, 2)
      f(3, 4)
    ---
    ((
      f(1, 2))+(
      f(3, 4)))
  """

  testCase """
    parenthesized special op
    ---
    items.reduce (is in), false
    ---
    items.reduce(((a,b) => b.includes(a)), false)
  """

  testCase """
    parenthesized custom op
    ---
    operator foo
    items.reduce (foo), 0
    ---

    items.reduce(foo, 0)
  """

  testCase """
    parenthesized negated custom op
    ---
    operator foo
    items.reduce (not foo), false
    ---

    items.reduce(((a,b) => !foo(a,b)), false)
  """

  testCase """
    parenthesized ! custom op
    ---
    operator foo
    items.reduce (!foo), false
    ---

    items.reduce(((a,b) => !foo(a,b)), false)
  """

  testCase """
    applied parenthesized custom op
    ---
    operator foo
    (foo) 1, 2
    ---

    foo(1, 2)
  """

describe "operator sections", ->
  testCase """
    left section
    ---
    items.map (1+)
    ---
    items.map((b => 1+b))
  """

  testCase """
    left section with spaces
    ---
    (1 +/*rhs*/)
    (1 in)
    (1 in )
    (1  in  )
    (1 not in)
    (1 !in)
    (1 instanceof)
    (1 !instanceof)
    (1 <?)
    (1 !<?)
    ---
    (b => 1 +/*rhs*/b);
    (b1 => 1 in b1);
    (b2 => 1 in b2);
    (b3 => 1  in  b3);
    (b4 => !(1 in b4));
    (b5 => !(1 in b5));
    (b6 => 1 instanceof b6);
    (b7 => !(1 instanceof b7));
    (b8 => 1 instanceof b8);
    (b9 => !(1 instanceof b9))
  """

  testCase """
    complex left section
    ---
    items.map (length * width + )
    ---
    items.map((b => length * width + b))
  """

  testCase """
    right section
    ---
    items.map (**2)
    ---
    items.map((a => a**2))
  """

  testCase """
    right section with spaces
    ---
    (/*lhs*/+ x)
    (in x)
    ( in x)
    (  in  x)
    (not in x)
    (!in x)
    (instanceof x)
    (!instanceof x)
    (<? x)
    (!<? x)
    ---
    (a => a/*lhs*/+ x);
    (a1 => a1 in x);
    (a2 => a2 in x);
    (a3 => a3  in  x);
    (a4 => !(a4 in x));
    (a5 => !(a5 in x));
    (a6 => a6 instanceof x);
    (a7 => !(a7 instanceof x));
    (a8 => a8 instanceof x);
    (a9 => !(a9 instanceof x))
  """

  testCase """
    +/- without space isn't section
    ---
    f (+x), (-x)
    ---
    f((+x), (-x))
  """

  testCase """
    ++/-- without space isn't section
    ---
    (⧺a)
    (—a)
    (++a)
    (--a)
    ---
    (++a);
    (--a);
    (++a);
    (--a)
  """

  testCase """
    complex right section
    ---
    items.map ( + length * width)
    ---
    items.map((a => a + length * width))
  """

  testCase """
    = section
    ---
    new Promise (resolve =)
    ---
    new Promise((b => resolve =b))
  """

  testCase """
    += section
    ---
    callback := (sum += /*num*/)
    ---
    const callback = (b => sum += /*num*/b)
  """

  testCase """
    & as not left section
    ---
    (& as number) + 1
    ---
    $ => ($ as number) + 1
  """

  testCase """
    & binary not left section
    ---
    operator foo
    (& foo 5) + 1
    ---
    ;
    $ => (foo($, 5)) + 1
  """
