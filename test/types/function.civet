{testCase, throws} from ../helper.civet

describe "[TS] function", ->
  testCase """
    -> has types
    ---
    const x = (a: number, b: number) : number ->
      return a + b
    ---
    const x = function(a: number, b: number) : number {
      return a + b
    }
  """

  testCase """
    => has types
    ---
    () : number => x
    (x) : number => x
    ---
    () : number => x;
    (x) : number => x
  """

  testCase """
    interface method with void => shorthand
    ---
    interface Array<T>
      listen(fu: =>): void
    ---
    interface Array<T> {
      listen(fu: ()=>void): void
    }
  """

  testCase """
    no parens is application
    ---
    x => y
    ---
    x(() => y)
  """

  testCase """
    type parameter without parameters
    ---
    function f<T>
      return 1 as T
    ---
    function f<T>() {
      return 1 as T
    }
  """

  testCase """
    'function' has types
    ---
    function add(a: number, b: number) : number
      return a + b
    ---
    function add(a: number, b: number) : number {
      return a + b
    }
  """

  testCase """
    function with indented return type
    ---
    function add<S, T>(a: S, b: T):
      S extends number ? T extends number ?
      number : string : string
      a + b
    ---
    function add<S, T>(a: S, b: T):
      S extends number ? T extends number ?
      number : string : string {
      return a + b
    }
  """

  testCase """
    argumentless without parens
    ---
    function f: void
      console.log 'hi'
    ---
    function f(): void {
      console.log('hi')
    }
  """

  testCase """
    longhand optional return
    ---
    function x(a: number, b?: number) ?: number
      return a + b
    ---
    function x(a: number, b?: number) : (undefined | number) {
      return a + b
    }
  """

  testCase """
    type parameters
    ---
    function add<T>(a: T, b: T) : T
      return a + b
    ---
    function add<T>(a: T, b: T) : T {
      return a + b
    }
  """

  testCase """
    arrow function type parameter
    ---
    const add = <T>(a: T, b: T) : T => a + b
    ---
    const add = <T,>(a: T, b: T) : T => a + b
  """

  testCase """
    arrow function type parameters
    ---
    const ignore = <T, U>(a: T, b: U) : void => null
    ---
    const ignore = <T, U>(a: T, b: U) : void => { null}
  """

  testCase """
    new arrow function type
    ---
    let type: new () => T
    ---
    let type: new () => T
  """

  testCase """
    abstract new arrow function type
    ---
    let type: abstract new () => T
    ---
    let type: abstract new () => T
  """

  throws """
    abstract requires new
    ---
    let type: abstract () => T
    ---
    ParseErrors: unknown:1:20 abstract function types must be constructors (abstract new)
  """

  testCase """
    arrow type asserts
    ---
    let type: (x: unknown) => asserts x
    ---
    let type: (x: unknown) => asserts x
  """

  testCase """
    arrow type asserts predicate
    ---
    let type: (x: unknown) => asserts x is T
    ---
    let type: (x: unknown) => asserts x is T
  """

  testCase """
    arrow type predicate
    ---
    let type: (x: unknown) => x is T
    ---
    let type: (x: unknown) => x is T
  """

  testCase """
    optional parameter
    ---
    const x = (a: number, b?: number) : number ->
      return a + b
    ---
    const x = function(a: number, b?: number) : number {
      return a + b
    }
  """

  testCase """
    optional return
    ---
    const x = (a: number, b?: number) ?: number ->
      return a + b
    ---
    const x = function(a: number, b?: number) : (undefined | number) {
      return a + b
    }
  """

  testCase """
    asserts
    ---
    assert = (value: unknown): asserts value ->
      throw new Error "falsey" unless value
    ---
    assert = function(value: unknown): asserts value {
      if (!value) { throw new Error("falsey") };return
    }
  """

  throws """
    optional asserts
    ---
    assert = (value: unknown)?: asserts value ->
      throw new Error "falsey" unless value
    ---
    ParseErrors: unknown:1:26 Can't use optional ?: syntax with asserts type
  """

  testCase """
    indented asserts
    ---
    function assert(value: unknown):
      asserts value
      throw new Error "falsey" unless value
    ---
    function assert(value: unknown):
      asserts value {
      if (!value) { throw new Error("falsey") };return
    }
  """

  testCase """
    asserts predicate
    ---
    assertString = (value: unknown): asserts value is string ->
      throw new Error "not string" unless value <? "string"
    ---
    assertString = function(value: unknown): asserts value is string {
      if (!(typeof value === "string")) { throw new Error("not string") };return
    }
  """

  testCase """
    type predicate
    ---
    isCat = (animal: Cat | Dog): animal is Cat ->
      return typeof animal.numberOfLives === 'number'
    ---
    isCat = function(animal: Cat | Dog): animal is Cat {
      return typeof animal.numberOfLives === 'number'
    }
  """

  testCase """
    async function type
    ---
    async function() : Promise<X> {
      return "x"
    }
    ---
    (async function() : Promise<X> {
      return "x"
    })
  """

  testCase """
    async generator function type
    ---
    async function* f() : X {
      return "x"
    }
    ---
    async function* f() : X {
      return "x"
    }
  """

  testCase """
    generator function type
    ---
    function* f() : X {
      return "x"
    }
    ---
    function* f() : X {
      return "x"
    }
  """

  testCase """
    no implicit return for void type
    ---
    (x): void ->
      console.log(x)
    ---
    (function(x): void {
      console.log(x)
    })
  """

  testCase """
    implicit body
    ---
    function noop()
    ---
    function noop(){}
  """

  testCase """
    overload with implicit body
    ---
    function noop(a: number, b: number): void
    function noop(a: string, b: string): void
    ---
    function noop(a: number, b: number): void
    function noop(a: string, b: string): void{}
  """

  testCase.js """
    overload with implicit body, JavaScript output
    ---
    function noop(a: number, b: number): void
    function noop(a: string, b: string): void
    ---

    function noop(a, b){}
  """

  testCase """
    export overload with implicit body
    ---
    export function noop(a: number, b: number): void
    export function noop(a: string, b: string): void
    ---
    export function noop(a: number, b: number): void
    export function noop(a: string, b: string): void{}
  """

  testCase """
    partial export overload has no implicit body
    ---
    function identity(x: number): number
    export function identity(x: string): string
      x
    ---
    function identity(x: number): number
    export function identity(x: string): string {
      return x
    }
  """

  testCase """
    overloads
    ---
    function add(a: number, b: number): number
    function add(a: string, b: string): string
    function add(a: number | string, b: number | string): number | string
      return a + b
    ---
    function add(a: number, b: number): number
    function add(a: string, b: string): string
    function add(a: number | string, b: number | string): number | string {
      return a + b
    }
  """

  testCase.js """
    overloads, JavaScript output
    ---
    function add(a: number, b: number): number
    function add(a: string, b: string): string
    function add(a: number | string, b: number | string): number | string
      return a + b
    ---


    function add(a, b) {
      return a + b
    }
  """

  testCase """
    complex
    ---
    declare function $C<T extends any[]>(...terms: { [I in keyof T]: Parser<T[I]> }): Parser<T[number]>
    ---
    declare function $C<T extends any[]>(...terms: { [I in keyof T]: Parser<T[I]> }): Parser<T[number]>
  """

  testCase """
    adds trailing comma with single type param
    ---
    <T>=> x
    <T>(x) => x
    ---
    <T,>() => x;
    <T,>(x) => x
  """

  testCase """
    const type parameters
    ---
    type HasNames = { names: readonly string[] };
    function getNamesExactly<const T extends HasNames>(arg: T): T["names"] {
    //                       ^^^^^
        return arg.names;
    }
    ---
    type HasNames = { names: readonly string[] };
    function getNamesExactly<const T extends HasNames>(arg: T): T["names"] {
    //                       ^^^^^
        return arg.names;
    }
  """

  describe "application", ->
    testCase """
      type arguments
      ---
      x<T>()
      ---
      x<T>()
    """

    testCase """
      type arguments with implicit parens
      ---
      x<T> y
      ---
      x<T>(y)
    """

    testCase """
      instantiation expressions
      ---
      y := x<T>
      z
      ---
      const y = x<T>
      z
    """

  describe "this type", ->
    testCase """
      interface method
      ---
      interface Object {
          state<T>(this: T): T
      }
      ---
      interface Object {
          state<T>(this: T): T
      }
    """

    testCase """
      interface method @ shorthand
      ---
      interface Object {
          state<T>(@: T): T
      }
      ---
      interface Object {
          state<T>(this: T): T
      }
    """

    testCase """
      plain function
      ---
      function (this: T) {}
      ---
      (function (this: T) {})
    """

    testCase """
      plain function with @ shorthand
      ---
      function (@: T) {}
      ---
      (function (this: T) {})
    """

    testCase """
      move this type to front
      ---
      function (x: T, @: T,) {}
      function (x: T, @: T) {}
      ---
      (function (this: T,x: T,) {});
      (function (this: T, x: T,) {})
    """

    throws """
      double this type
      ---
      function (this: T, @: T) {}
      ---
      ParseErrors: unknown:1:19 Only one typed this parameter is allowed
    """

  describe ":: typing of parameters", ->
    testCase """
      binding array pattern params with types
      ---
      [a:: A, b:: B] ->
        a
      [a:: A, b:: B] =>
        a
      ---
      (function([a, b]: [ A, B]) {
        return a
      });
      ([a, b]: [ A, B]) => {
        return a
      }
    """

    testCase """
      binding object pattern params with types
      ---
      {a:: A, b:: B} ->
        a
      {a:: A, b:: B} =>
        a
      ---
      (function({a, b}: {a: A, b: B}) {
        return a
      });
      ({a, b}: {a: A, b: B}) => {
        return a
      }
    """

  describe "automatic Promise in async", ->
    testCase """
      thin arrow
      ---
      async :void ->
      ---
      (async function():Promise<void> {})
    """

    testCase """
      thick arrow
      ---
      async :void =>
      ---
      async ():Promise<void> => {}
    """

    testCase """
      function
      ---
      async function f(): void {}
      ---
      async function f(): Promise<void> {}
    """

    testCase """
      implicit thin arrow
      ---
      f := :number -> await Promise.resolve 5
      ---
      const f = async function():Promise<number> { return await Promise.resolve(5) }
    """

    testCase """
      implicit thick arrow
      ---
      f := :number => await Promise.resolve 5
      ---
      const f = async ():Promise<number> => await Promise.resolve(5)
    """

    testCase """
      implicit function
      ---
      function f(): number
        await Promise.resolve 5
      ---
      async function f(): Promise<number> {
        return await Promise.resolve(5)
      }
    """
