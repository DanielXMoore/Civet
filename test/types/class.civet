{testCase} from ../helper.civet

describe "[TS] class", ->
  testCase """
    example
    ---
    class UserAccount {
      name: string
      id: number

      constructor(name: string, id: number) {
        this.name = name
        this.id = id
      }
    }
    ---
    class UserAccount {
      name: string
      id: number

      constructor(name: string, id: number) {
        this.name = name
        this.id = id
      }
    }
  """

  testCase """
    method definition
    ---
    class UserAccount
      register(): number
        return this.id
    ---
    class UserAccount {
      register(): number {
        return this.id
      }
    }
  """

  testCase """
    nested
    ---
    class UserAccount
      name: string
      id: number

      constructor(name: string, id: number)
        @name = name
        @id = id
    ---
    class UserAccount {
      name: string
      id: number

      constructor(name: string, id: number) {
        this.name = name
        this.id = id
      }
    }
  """

  testCase """
    const assignment becomes readonly field
    ---
    class A
      x := 3
    ---
    class A {
      readonly x = 3
    }
  """

  testCase """
    readonly function field
    ---
    class X
      f: MyFuncType := -> return "hi"
    ---
    class X {
      readonly f: MyFuncType = function() { return "hi" }
    }
  """

  testCase """
    readonly static field
    ---
    class X
      static x := 3
    ---
    class X {
      static readonly x = 3
    }
  """

  testCase """
    readonly static field
    ---
    class X
      @x := 3
    ---
    class X {
      static readonly x = 3
    }
  """
