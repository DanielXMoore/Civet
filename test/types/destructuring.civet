{testCase, throws} from ../helper.civet

describe "[TS] destructuring", ->
  testCase """
    basic
    ---
    const hello = ({ first, last }: Person) =>
      return `Hello ${first} ${last}!`
    ---
    const hello = ({ first, last }: Person) => {
      return `Hello ${first} ${last}!`
    }
  """

  describe ":: typing of properties", ->
    testCase """
      simple
      ---
      { first:: string, last?:: string } := person
      ---
      const { first, last }: { first: string, last?: string,} = person
    """

    testCase """
      renamed
      ---
      { first: f:: string, last: l ?:: string } := person
      ---
      const { first: f, last: l }: { first: string, last ?: string,} = person
    """

    testCase """
      at
      ---
      first := 'Hello'
      { @first:: string, @last?:: string } := person
      ---
      const first = 'Hello'
      const {first:  first1, last }: { first: string, last?: string,} = person;this.first = first1;this.last = last;
    """

    testCase """
      rest
      ---
      { first:: string, ...rest:: { last?: string } } := person
      ---
      const { first, ...rest }: { first: string,} & ({ last?: string }) = person
    """

    throws """
      pin
      ---
      { ^foo:: string } := person
      ---
      ParseErrors: unknown:1:7 Pinned properties cannot have type annotations
    """

    testCase """
      function parameter
      ---
      function Component({
        first:: string
        last: l:: string
        ...rest:: OtherProps
      })
      ---
      function Component({
        first,
        last: l,
        ...rest
      }: {
        first: string,
        last: string,} & (OtherProps)){}
    """
