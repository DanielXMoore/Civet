{testCase, throws} from ../helper.civet

describe "[TS] let declaration", ->
  testCase """
    array of empty object type
    ---
    let x: {}[]
    ---
    let x: {}[]
  """

  testCase """
    trailing JSX is not treated as type arguments
    ---
    let svg: SVGSVGElement
    <header>
    <svg ref={svg!}>
    ---
    let svg: SVGSVGElement
    <>
    <header />
    <svg ref={svg!} />
    </>
  """

  testCase """
    non-null assertion
    ---
    let ref!: HTMLDivElement
    ---
    let ref!: HTMLDivElement
  """

  testCase """
    non-null assertion
    ---
    let untyped!
    ---
    let untyped!
  """, wrapper: ''

  throws """
    forbid ?!
    ---
    let ref?!
    ---
    ParseError
  """

  testCase """
    let ? sets to undefined
    ---
    let n?
    ---
    let n = undefined
  """

  testCase """
    let ? with initializer
    ---
    let a? = true
    let b? = 5
    let c? = 5n
    let d? = "hello"
    let e? = `hello`
    let f? = /hello/
    let g? = x
    let h? = x.y[z]
    ---
    let a : undefined | boolean= true
    let b : undefined | number= 5
    let c : undefined | bigint= 5n
    let d : undefined | string= "hello"
    let e : undefined | string= `hello`
    let f : undefined | RegExp= /hello/
    let g : undefined | (typeof x)= x
    let h : undefined | (typeof x.y[z])= x.y[z]
  """

  throws """
    let ? with null initializer
    ---
    let a? = null
    ---
    ParseErrors: unknown:1:6 Optional type can't be inferred from null
  """

  throws """
    let ? with undefined initializer
    ---
    let a? = undefined
    ---
    ParseErrors: unknown:1:6 Optional type can't be inferred from undefined
  """

  throws """
    let ? with complex initializer
    ---
    let c? = x.y()
    ---
    ParseErrors: unknown:1:6 Optional type can only be inferred from literals or member expressions, not CallExpression
  """

  testCase """
    let ?: allows for undefined
    ---
    let n?: number
    ---
    let n: undefined | number
  """

  testCase """
    shorthand let ?: allows for undefined
    ---
    n?: number .= undefined
    ---
    let n: undefined | number = undefined
  """

  testCase """
    let ?: with spaces
    ---
    let n ? : number
    ---
    let n  : undefined | number
  """

  testCase """
    let with indented type
    ---
    let x:
      number | string
    ---
    let x:
      number | string
  """

  testCase """
    let with one-line conditional type
    ---
    let x: if T extends Animal then string else number
    ---
    let x: (T extends Animal? string : number)
  """

  testCase """
    let with indented conditional type
    ---
    let x:
      if T extends Animal
        string
      else
        number
    ---
    let x:
      (T extends Animal?
        string
      :
        number)
  """
