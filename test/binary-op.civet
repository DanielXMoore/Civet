{testCase, throws, evalsTo, wrapper} from ./helper.civet

describe "binary operations", ->
  testCase """
    bitwise shift
    ---
    a << b
    a >> b
    a >>> b
    ---
    a << b
    a >> b
    a >>> b
  """

  testCase """
    multiplicative
    ---
    a * b
    a / b
    a % b
    ---
    a * b
    a / b
    a % b
  """

  testCase """
    positive modulo
    ---
    a %% b
    ---
    var modulo = ((a: number, b: number) => (a % b + b) % b) as ((a: number, b: number) => number) & ((a: bigint, b: bigint) => bigint);
    modulo(a, b)
  """

  testCase.js """
    positive modulo JS
    ---
    a %% b
    ---
    var modulo = (a, b) => (a % b + b) % b;
    modulo(a, b)
  """

  testCase """
    positive modulo not relational
    ---
    a %% b %% c
    ---
    var modulo = ((a: number, b: number) => (a % b + b) % b) as ((a: number, b: number) => number) & ((a: bigint, b: bigint) => bigint);
    modulo(modulo(a, b), c)
  """

  testCase """
    positive modulo precedence
    ---
    x + a %% b
    ---
    var modulo = ((a: number, b: number) => (a % b + b) % b) as ((a: number, b: number) => number) & ((a: bigint, b: bigint) => bigint);
    x + modulo(a, b)
  """

  testCase """
    floor divide
    ---
    a %/ b
    a ÷ b
    ---
    var div: (a: number, b: number) => number = (a, b) => Math.floor(a / b);
    div(a, b)
    div(a, b)
  """

  testCase """
    floor divide precedence
    ---
    x + a %/ b
    ---
    var div: (a: number, b: number) => number = (a, b) => Math.floor(a / b);
    x + div(a, b)
  """

  testCase """
    additive
    ---
    a + b
    a - b
    ---
    a + b
    a - b
  """

  testCase """
    additive
    ---
    a
    - b
    -c
    ---
    a
    - b;
    -c
  """

  testCase """
    relational
    ---
    a < b
    a > b
    a <= b
    a >= b
    a == b
    a === b
    a != b
    a !== b
    a <? b
    a !<? b
    ---
    a < b
    a > b
    a <= b
    a >= b
    a == b
    a === b
    a != b
    a !== b
    a instanceof b
    !(a instanceof b)
  """

  testCase """
    relational chaining
    ---
    a < b === c < d
    ---
    a < b && b === c && c < d
  """

  // NOTE: most chaining tests are in chained-comparisons.civet

  testCase """
    snug <
    ---
    x<y
    x<y>z
    ---
    x<y
    x<y && y>z
  """

  testCase """
    snug < with literal
    ---
    x<y
    x<y>0
    ---
    x<y
    x<y && y>0
  """

  testCase """
    snug <?
    ---
    x<?y
    x!<?y
    ---
    x instanceof y
    !(x instanceof y)
  """

  testCase """
    typeof shorthand
    ---
    a <? "string"
    a !<? "string"
    a instanceof "string"
    a not instanceof "string"
    a !instanceof "string"
    ---
    typeof a === "string"
    typeof a !== "string"
    typeof a === "string"
    typeof a !== "string"
    typeof a !== "string"
  """

  testCase """
    converts is to ===
    ---
    a is b
    ---
    a === b
  """

  testCase """
    converts is not to !==
    ---
    a is not b
    ---
    a !== b
  """

  describe "objectIs", ->
    testCase """
      converts is to Object.is
      ---
      "civet objectIs"
      a is b
      ---
      var is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, b)
    """

    testCase """
      converts is not to !Object.is
      ---
      "civet objectIs"
      a is not b
      ---
      var is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      !is(a, b)
    """

    testCase """
      Object.is adds as const for boolean literals
      ---
      "civet objectIs"
      a is true
      ---
      var is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, true as const)
    """

    testCase """
      Object.is adds as const for numeric literals
      ---
      "civet objectIs"
      a is 0
      ---
      var is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, 0 as const)
    """

    testCase """
      Object.is adds as const for string literals
      ---
      "civet objectIs"
      a is "hello"
      ---
      var is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, "hello" as const)
    """

    testCase """
      Object.is does not add as const for null literals
      ---
      "civet objectIs"
      a is null is undefined
      ---
      var is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, null) && is(null, undefined)
    """

    testCase """
      Object.is precedence
      ---
      "civet objectIs"
      a % 2 is 0 is not b*c+d
      ---
      var is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a % 2, 0 as const) && !is(0 as const, b*c+d)
    """

  testCase """
    in operator
    ---
    a in b
    ---
    a in b
  """

  testCase """
    not in operator
    ---
    a not in b
    ---
    !(a in b)
  """

  testCase """
    !in operator
    ---
    a !in b
    ---
    !(a in b)
  """

  testCase """
    not in after logical
    ---
    v and v.type is not in ["CallExpression", "MemberExpression", "Identifier"]
    ---
    v && !["CallExpression", "MemberExpression", "Identifier"].includes(v.type)
  """

  describe "branded", ->
    wrapper """
      class X {
        #a = 0
        f() {
          CODE
        }
      }
    """

    testCase """
      branded in
      ---
      #a in b
      ---
      #a in b
    """

    testCase """
      branded not in
      ---
      #a not in b
      ---
      !(#a in b)
    """

  testCase """
    is in operator
    ---
    a is in b
    ---
    b.includes(a)
  """

  testCase """
    is not in operator
    ---
    a is not in b
    ---
    !b.includes(a)
  """

  testCase """
    is !in operator
    ---
    a is !in b
    ---
    !b.includes(a)
  """

  testCase """
    Unicode is in operator
    ---
    a ∈ b ∉ c ∋ d ∌ e
    ---
    b.includes(a) && !c.includes(b) && c.includes(d) && !d.includes(e)
  """

  testCase """
    is in precedence
    ---
    a+b is in c*d
    ---
    (c*d).includes(a+b)
  """

  testCase """
    is in chaining
    ---
    a+b is in c*d is in e%f+g
    ---
    let ref;(ref = c*d).includes(a+b) && (e%f+g).includes(ref)
  """

  testCase """
    is in long chains
    ---
    x+y+z is in a is in b
    ---
    a.includes(x+y+z) && b.includes(a)
  """

  testCase """
    ++ operator
    ---
    a ++ b
    ---
    a.concat(b)
  """

  testCase """
    Unicode ++ operator
    ---
    a ⧺ b
    ---
    a.concat(b)
  """

  testCase """
    tight ++ operator
    ---
    a++b
    ---
    a.concat(b)
  """

  testCase """
    tight Unicode ++ operator
    ---
    a⧺b
    ---
    a.concat(b)
  """

  testCase """
    ++ chain
    ---
    0 ++ [1,2,3]++4
    ---
    0..concat([1,2,3]).concat(4)
  """

  // #1456
  testCase """
    ++ less tight than as
    ---
    arr as string[] ++ y
    ---
    (arr as string[]).concat(y)
  """

  testCase """
    tight bitwise
    ---
    a|b
    a&b
    a^b
    ---
    a|b
    a&b
    a^b
  """

  testCase """
    bitwise
    ---
    a | b
    a & b
    a ^ b
    ---
    a | b
    a & b
    a ^ b
  """

  testCase """
    logical
    ---
    a || b
    a or b
    a && b
    a and b
    a ?? b
    ---
    a || b
    a || b
    a && b
    a && b
    a ?? b
  """

  testCase """
    multiple logical
    ---
    a || b || c
    a && b && c
    a || b && c
    a && b || c
    ---
    a || b || c
    a && b && c
    a || b && c
    a && b || c
  """

  testCase """
    xor
    ---
    a^^b
    a ^^ b
    a xor b
    a^^=b
    ---
    type Falsy = false | 0 | '' | 0n | null | undefined;
    var xor: <A, B>(a: A, b: B) => A extends Falsy ? B : B extends Falsy ? A : (false | (A & Falsy extends never ? never : B) | (B & Falsy extends never ? never : A)) = (a, b) => (a ? !b && a : b) as any;
    xor(a,b)
    xor(a, b)
    xor(a, b)
    a=xor(a, b)
  """

  it "xor correct", ->
    evalsTo "'x' xor 'y'", false
    evalsTo "'x' xor ''", 'x'
    evalsTo "'' xor 'x'", 'x'
    evalsTo "'' xor 0", 0
    evalsTo "0 xor ''", ''

  testCase """
    xnor
    ---
    a!^b
    a!^^b
    a !^ b
    a !^^ b
    a xnor b
    ---
    type Falsy = false | 0 | '' | 0n | null | undefined;
    var xnor: <A, B>(a: A, b: B) => A & Falsy extends never ? B : (true | (B extends Falsy ? never : A) | (A extends Falsy ? never : B)) = (a, b) => (a ? b : !b || a) as any;
    xnor(a,b)
    xnor(a,b)
    xnor(a, b)
    xnor(a, b)
    xnor(a, b)
  """

  it "xnor correct", ->
    evalsTo "'x' xnor 'y'", 'y'
    evalsTo "'x' xnor ''", ''
    evalsTo "'' xnor 'x'", ''
    evalsTo "'' xnor 0", true
    evalsTo "0 xnor ''", true

  testCase """
    xor precedence
    ---
    a < b xor c < d
    a * b xor c < d xor e + f
    a and b xor c and d
    a or b xor c or d
    a is in b xor c is in d
    ---
    type Falsy = false | 0 | '' | 0n | null | undefined;
    var xor: <A, B>(a: A, b: B) => A extends Falsy ? B : B extends Falsy ? A : (false | (A & Falsy extends never ? never : B) | (B & Falsy extends never ? never : A)) = (a, b) => (a ? !b && a : b) as any;
    xor(a < b, c < d)
    xor(xor(a * b, c < d), e + f)
    xor(a && b, c && d)
    a || xor(b, c) || d
    xor(b.includes(a), d.includes(c))
  """

  testCase """
    space before newline
    ---
    a ||\u0020
    b
    ---
    a ||\u0020
    b
  """

  testCase """
    weird spacing
    ---
    a
        + 123
     /*heyy*/    + 1

           + b
    ---
    a
        + 123
     /*heyy*/    + 1

           + b
  """

  testCase """
    snug newline
    ---
    a+
    b
    ---
    a+
    b
  """

  testCase """
    spaced newline
    ---
    a +
    b
    ---
    a +
    b
  """

  testCase """
    not-dedented binary op
    ---
    a
    + b
    * c
    ---
    a
    + b
    * c
  """

  // #598
  testCase """
    nested after binary op
    ---
    name := optionalName ??
      if loaded
        file.name
      else
        throw new Error
    ---
    const name = optionalName ??
      (()=>{if (loaded) {
        return file.name
      }
      else {
        throw new Error
      }})()
  """

  testCase """
    throw expression in logical or binary op
    ---
    a or throw new Error
    ---
    a || (()=>{throw new Error})()
  """

  testCase """
    await in throw expression
    ---
    a or throw await makeError()
    ---
    a || (await (async ()=>{throw await makeError()})())
  """

  testCase """
    late assignments
    ---
    x = 3 + y = 2 + z = 1
    ---
    x = 3 + (y = 2 + (z = 1))
  """

  testCase """
    arrow function fallbacks
    ---
    callback ?? (x) => console.log 'finished:', x
    ---
    callback ?? ((x) => console.log('finished:', x))
  """

  testCase """
    instanceof
    ---
    a instanceof b
    ---
    a instanceof b
  """

  testCase """
    not instanceof
    ---
    a not instanceof b
    ---
    !(a instanceof b)
  """

  testCase """
    !instanceof
    ---
    a !instanceof b
    ---
    !(a instanceof b)
  """

  testCase """
    comma operator outside parens
    ---
    f(), g()
    ---
    f(), g()
  """

  testCase """
    comma operator in parens
    ---
    (f(), g())
    ---
    (f(), g())
  """

  testCase """
    Unicode computation
    ---
    (a ≪ b ≫ c ⋙ d ‖ e) ⁇ f
    ---
    (a << b >> c >>> d || e) ?? f
  """

  testCase """
    Unicode relations
    ---
    a ≤ b ≥ c ≠ d ≢ e ≡ f ≣ g
    ---
    a <= b && b >= c && c != d && d !== e && e == f && f === g
  """

  testCase """
    long Unicode relations
    ---
    a ⩵ b ⩶ c
    ---
    a == b && b === c
  """

  describe "nesting", ->
    testCase """
      same line within function call
      ---
      f x
      + y
      ---
      f(x)
      + y
    """

    testCase """
      strictly indented within function call
      ---
      f x
        + y
      ---
      f(x
        + y)
    """

describe "custom identifier infix operators", ->
  testCase """
    bless
    ---
    operator foo
    a foo b
    a bar b
    ---

    foo(a, b)
    a(bar(b))
  """

  testCase """
    chain
    ---
    operator foo
    a foo b foo c
    ---

    foo(foo(a, b), c)
  """

  testCase """
    chain over multiple lines
    ---
    operator foo
    a
      foo b
      foo c
    ---


      foo(
      foo(a, b), c)
  """

  testCase """
    use operator as leading function
    ---
    operator foo
    foo a, b
    foo c, d
    ---

    foo(a, b)
    foo(c, d)
  """

  testCase """
    use operator as function
    ---
    operator foo
    a foo foo(b, c)
    ---

    foo(a, foo(b, c))
  """

  testCase """
    multiple bless
    ---
    operator foo, bar
    a foo b bar c
    ---

    bar(foo(a, b), c)
  """

  testCase """
    function operator
    ---
    operator min(a, b)
      Math.min a, b
    a min b
    ---
    function min(a, b) {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    operator function operator
    ---
    operator function min(a, b)
      Math.min a, b
    a min b
    ---
    function min(a, b) {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    function operator with types
    ---
    operator min(a: number, b: number): number
      Math.min a, b
    a min b
    ---
    function min(a: number, b: number): number {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    function operator with braces
    ---
    operator min(a, b) {
      Math.min a, b
    }
    a min b
    ---
    function min(a, b) {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    export function operator
    ---
    export operator min(a, b)
      Math.min a, b
    a min b
    ---
    export function min(a, b) {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    implicit operator generator
    ---
    operator seq(a, b)
      yield a
      yield b
      return
    a seq b
    ---
    function* seq(a, b) {
      yield a
      yield b
      return
    }
    seq(a, b)
  """

  testCase """
    explicit operator generator
    ---
    operator* seq(a, b)
      yield a
      yield b
      return
    a seq b
    ---
    function* seq(a, b) {
      yield a
      yield b
      return
    }
    seq(a, b)
  """

  testCase """
    implicit async operator
    ---
    operator seq(a, b)
      await a
      await b
      return
    a seq b
    ---
    async function seq(a, b) {
      await a
      await b
      return
    }
    seq(a, b)
  """

  testCase """
    explicit async operator
    ---
    async operator seq(a, b)
      await a
      await b
      return
    a seq b
    ---
    async function seq(a, b) {
      await a
      await b
      return
    }
    seq(a, b)
  """

  testCase """
    let operator
    ---
    operator let min = Math.min, max = Math.max
    a min b max c
    ---
    let min = Math.min, max = Math.max
    max(min(a, b), c)
  """

  testCase """
    const operator
    ---
    operator const {min, max} = Math
    a min b max c
    ---
    const {min, max} = Math
    max(min(a, b), c)
  """

  testCase """
    const shorthand operator
    ---
    operator {min, max} := Math
    a min b max c
    ---
    const {min, max} = Math
    max(min(a, b), c)
  """

  testCase """
    import operator
    ---
    import { operator min, max } from 'ops'
    a min b max c
    ---
    import { min, max } from 'ops'
    min(a, b(max(c)))
  """

  testCase """
    import operators
    ---
    import { operator min, operator max } from 'ops'
    a min b max c
    ---
    import { min, max } from 'ops'
    max(min(a, b), c)
  """

  testCase """
    bulk import operators
    ---
    import operator { min, max } from 'ops'
    a min b max c
    ---
    import { min, max } from 'ops'
    max(min(a, b), c)
  """

  testCase """
    bulk implicit import operators
    ---
    operator { min, max } from 'ops'
    a min b max c
    ---
    import { min, max } from 'ops'
    max(min(a, b), c)
  """

  testCase """
    operator in parens acts as function
    ---
    operator foo
    x (foo) y
    ---

    x(foo(y))
  """

  testCase """
    not operator
    ---
    operator foo
    x not foo y
    ---

    !foo(x, y)
  """

  testCase """
    ! operator
    ---
    operator foo
    x !foo y
    ---

    !foo(x, y)
  """

  testCase """
    operator precedence
    ---
    operator foo
    x == a+b foo c*d
    a+b foo c*d < x
    a or b foo c and d
    ---

    x == foo(a+b, c*d)
    foo(a+b, c*d) < x
    a || foo(b, c) && d
  """

  testCase """
    shadowed in arguments
    ---
    operator foo (a, b)
      a

    (foo) => arr[foo]
    ---
    function foo (a, b) {
      return a
    };

    (foo) => arr[foo]
  """

  describe "custom precedence", ->
    testCase """
      same as symbol
      ---
      operator mul same (*) (a, b)
        a*b
      a+b*c mul d*e+f
      ---
      function mul (a, b) {
        return a*b
      }
      a+mul(b*c, d)*e+f
    """

    testCase """
      same as symbol, declaration
      ---
      operator same (*) mul := (a, b) => a*b
      a+b*c mul d*e+f
      ---
      const mul = (a, b) => a*b
      a+mul(b*c, d)*e+f
    """

    testCase """
      tighter
      ---
      operator mul tighter (*)
      operator MUL tighter mul
      a+b*c mul d MUL e mul f*g+h
      ---


      a+b*mul(mul(c, MUL(d, e)), f)*g+h
    """

    testCase """
      looser
      ---
      operator mul looser (*)
      operator MUL looser mul
      a+b*c mul d MUL e mul f*g+h
      ---


      a+MUL(mul(b*c, d), mul(e, f*g))+h
    """

    testCase """
      same as identifier
      ---
      operator foo same (+)
      operator bar same foo
      operator baz same (foo)
      a foo b bar c baz d foo e bar f baz g
      ---



      baz(bar(foo(baz(bar(foo(a, b), c), d), e), f), g)
    """

    throws """
      invalid identifier
      ---
      operator A looser X
      ---
      ParseErrors: unknown:1:1 Precedence refers to unknown operator X
    """

    testCase """
      import precedence
      ---
      import { operator foo tighter (+) as bar } from 'ops'
      a+b bar c+d
      ---
      import { foo as bar } from 'ops'
      a+bar(b, c)+d
    """

    testCase """
      general import precedence
      ---
      operator tighter (+) { foo, bar looser (+) } from 'ops'
      a+b foo c+d bar e
      ---
      import { foo, bar } from 'ops'
      bar(a+foo(b, c)+d, e)
    """

  describe "custom associativity", ->
    testCase """
      left
      ---
      operator foo left
      a foo b foo c
      ---

      foo(foo(a, b), c)
    """

    testCase """
      right
      ---
      operator foo right
      a foo b foo c
      ---

      foo(a, foo(b, c))
    """

    testCase """
      non ok
      ---
      operator foo non
      a foo b
      ---

      foo(a, b)
    """

    throws """
      non overload
      ---
      operator foo non
      a foo b foo c
      ---
      ParseErrors: unknown:2:9 non-associative operator foo used at same precedence level without parenthesization
    """

    throws """
      non same overload
      ---
      operator foo non same (+)
      a foo b + c
      ---
      ParseErrors: unknown:2:9 non-associative operator foo used at same precedence level without parenthesization
    """

    testCase """
      relational
      ---
      operator foo relational
      a < b foo c foo d instanceof e
      ---

      a < b && foo(b, c) && foo(c, d) && d instanceof e
    """

    testCase """
      arguments
      ---
      operator foo arguments
      a foo b foo c
      ---

      foo(a, b , c)
    """

    throws """
      arguments same overload left
      ---
      operator foo arguments same (+)
      a + b foo c
      ---
      ParseErrors: unknown:2:13 arguments operator foo used at same precedence level as + to the left
    """

    throws """
      arguments same overload right
      ---
      operator foo arguments same (+)
      a foo b + c
      ---
      ParseErrors: unknown:2:13 arguments operator foo used at same precedence level as + to the right
    """

    testCase """
      operator named right
      ---
      operator right
      a right b right c
      ---

      right(right(a, b), c)
    """

    testCase """
      operator named right explicitly left
      ---
      operator right left
      a right b right c
      ---

      right(right(a, b), c)
    """

  describe "operators config", ->
    testCase """
      names in pragma
      ---
      "civet operators=min,max"
      x min u max l
      ---
      max(min(x, u), l)
    """

    testCase """
      names in config
      ---
      x min u max l
      ---
      max(min(x, u), l)
    """, parseOptions: operators: ["min", "max"]

    testCase """
      defaults in config
      ---
      x min u max l
      ---
      max(min(x, u), l)
    """, parseOptions: operators:
      min: undefined
      max: ''

    testCase """
      associativity in config
      ---
      a foo b foo c
      ---
      foo(a, foo(b, c))
    """, parseOptions: operators: foo: "right"

    testCase """
      precedence in config
      ---
      a*b foo c+d foo e*f
      ---
      foo(foo(a*b, c)+d, e*f)
    """, parseOptions: operators: foo: "same (+)"

    testCase """
      associativity and precedence in config
      ---
      a<b foo c<d foo e<f
      ---
      foo(a<b, c<d , e<f)
    """, parseOptions: operators: foo: "looser (<) arguments"

    testCase """
      duplicate operator blessing
      ---
      operator foo
      a foo b foo c
      ---

      foo(foo(a, b), c)
    """, parseOptions: operators: ['foo']

    testCase """
      duplicate operator blessing with defaults
      ---
      operator foo
      a<b foo c<d foo e<f
      ---

      foo(a<b, c<d , e<f)
    """, parseOptions: operators: foo: "looser (<) arguments"

    testCase """
      duplicate operator blessing with precedence override
      ---
      operator foo looser (<)
      a<b foo c<d foo e<f
      ---

      foo(a<b, c<d , e<f)
    """, parseOptions: operators: foo: "tighter (<) arguments"

    throws """
      invalid config
      ---
      foo
      ---
      Error: operators configuration must be an object or array
    """,
      // @ts-ignore Testing passing in wrong type
      parseOptions: operators: 42

    throws """
      invalid config object
      ---
      foo
      ---
      ParseError: operator config for foo:1:1 Failed to parse
    """, parseOptions: operators: foo: "foo"

describe "is like pattern matching test", ->
  testCase """
    basic
    ---
    x is like [1, x, 3]
    ---
    function len<T extends readonly unknown[], N extends number>(arr: T, length: N): arr is T & { length: N } { return arr.length === length }
    ;(Array.isArray(x) && len(x, 3) && x[0] === 1 && x[2] === 3)
  """

  testCase """
    multiple patterns
    ---
    x is like [1, x, 3], []
    ---
    function len<T extends readonly unknown[], N extends number>(arr: T, length: N): arr is T & { length: N } { return arr.length === length }
    ;(Array.isArray(x) && len(x, 3) && x[0] === 1 && x[2] === 3 || Array.isArray(x) && len(x, 0))
  """

  testCase """
    ref
    ---
    f() is like [1, x, 3]
    ---
    function len<T extends readonly unknown[], N extends number>(arr: T, length: N): arr is T & { length: N } { return arr.length === length }
    let m;(m = f(),Array.isArray(m) && len(m, 3) && m[0] === 1 && m[2] === 3)
  """

  testCase """
    assigned
    ---
    isRef := node is like {type: "Ref"}
    ---
    const isRef = (typeof node === 'object' && node != null && 'type' in node && node.type === "Ref")
  """

  testCase """
    assigned with ref
    ---
    isRef := f() is like {type: "Ref"}
    ---
    let m;const isRef = (m = f(),typeof m === 'object' && m != null && 'type' in m && m.type === "Ref")
  """

  testCase """
    binary ops in left-hand side
    ---
    x + y+z is like /abc/, /def/
    ---
    let m;(m = x + y+z,typeof m === 'string' && /abc/.test(m) || typeof m === 'string' && /def/.test(m))
  """

  testCase """
    conditional in left-hand side
    ---
    x < y is like /abc/
    ---
    x < y && (typeof y === 'string' && /abc/.test(y))
  """

  testCase """
    conditionals in left-hand side
    ---
    x < y < z? is like /abc/
    ---
    x < y && z != null && y < z && (typeof z === 'string' && /abc/.test(z))
  """

  testCase """
    indented form
    ---
    bare and
      expressions is like []
    ---
    function len<T extends readonly unknown[], N extends number>(arr: T, length: N): arr is T & { length: N } { return arr.length === length }
    bare &&
      (Array.isArray(expressions) && len(expressions, 0))
  """

  testCase """
    is not like
    ---
    x is not like {type: "Ref"}
    ---
    !(typeof x === 'object' && x != null && 'type' in x && x.type === "Ref")
  """

  testCase """
    is !like
    ---
    x is !like {type: "Ref"}
    ---
    !(typeof x === 'object' && x != null && 'type' in x && x.type === "Ref")
  """

  testCase """
    section
    ---
    (is like {type: "Ref"})
    ---
    (a => (typeof a === 'object' && a != null && 'type' in a && a.type === "Ref"))
  """
