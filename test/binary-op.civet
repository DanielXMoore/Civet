{testCase, throws} from ./helper.civet

describe "binary operations", ->
  testCase """
    bitwise shift
    ---
    a << b
    a >> b
    a >>> b
    ---
    a << b
    a >> b
    a >>> b
  """

  testCase """
    multiplicative
    ---
    a * b
    a / b
    a % b
    ---
    a * b
    a / b
    a % b
  """

  testCase """
    positive modulo
    ---
    a %% b
    ---
    const modulo: (a: number, b: number) => number = (a, b) => (a % b + b) % b;
    modulo(a, b)
  """

  testCase """
    positive modulo not relational
    ---
    a %% b %% c
    ---
    const modulo: (a: number, b: number) => number = (a, b) => (a % b + b) % b;
    modulo(modulo(a, b), c)
  """

  testCase """
    additive
    ---
    a + b
    a - b
    ---
    a + b
    a - b
  """

  testCase """
    relational
    ---
    a < b
    a > b
    a <= b
    a >= b
    a == b
    a === b
    a != b
    a !== b
    a <? b
    a !<? b
    ---
    a < b
    a > b
    a <= b
    a >= b
    a == b
    a === b
    a != b
    a !== b
    a instanceof b
    !(a instanceof b)
  """

  testCase """
    typeof shorthand
    ---
    a <? "string"
    a !<? "string"
    a instanceof "string"
    a not instanceof "string"
    ---
    typeof a === "string"
    typeof a !== "string"
    typeof a === "string"
    typeof a !== "string"
  """

  testCase """
    converts is to ===
    ---
    a is b
    ---
    a === b
  """

  testCase """
    converts is not to !==
    ---
    a is not b
    ---
    a !== b
  """

  describe "objectIs", ->
    testCase """
      converts is to Object.is
      ---
      "civet objectIs"
      a is b
      ---
      const is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, b)
    """

    testCase """
      converts is not to !Object.is
      ---
      "civet objectIs"
      a is not b
      ---
      const is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      !is(a, b)
    """

    testCase """
      Object.is adds as const for boolean literals
      ---
      "civet objectIs"
      a is true
      ---
      const is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, true as const)
    """

    testCase """
      Object.is adds as const for numeric literals
      ---
      "civet objectIs"
      a is 0
      ---
      const is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, 0 as const)
    """

    testCase """
      Object.is adds as const for string literals
      ---
      "civet objectIs"
      a is "hello"
      ---
      const is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, "hello" as const)
    """

    testCase """
      Object.is does not add as const for null literals
      ---
      "civet objectIs"
      a is null is undefined
      ---
      const is: { <B, A extends B> (a: A, b: B): b is A, <A, B> (a: A, b: B): a is A & B } = Object.is as any;
      is(a, null) && is(null, undefined)
    """

  testCase """
    in operator
    ---
    a in b
    ---
    a in b
  """

  testCase """
    not in operator
    ---
    a not in b
    ---
    !(a in b)
  """

  testCase """
    is in operator
    ---
    a is in b
    ---
    b.includes(a)
  """

  testCase """
    is not in operator
    ---
    a is not in b
    ---
    !b.includes(a)
  """

  testCase """
    bitwise
    ---
    a | b
    a & b
    a ^ b
    ---
    a | b
    a & b
    a ^ b
  """

  testCase """
    logical
    ---
    a || b
    a or b
    a && b
    a and b
    a ?? b
    ---
    a || b
    a || b
    a && b
    a && b
    a ?? b
  """

  testCase """
    multiple logical
    ---
    a || b || c
    a && b && c
    a || b && c
    a && b || c
    ---
    a || b || c
    a && b && c
    a || b && c
    a && b || c
  """

  testCase """
    xor
    ---
    a^^b
    a ^^ b
    a xor b
    ---
    const xor: (a: unknown, b: unknown) => boolean = (a, b) => a && b ? false : a || b;
    xor(a,b)
    xor(a, b)
    xor(a, b)
  """

  testCase """
    xnor
    ---
    a!^b
    a!^^b
    a !^ b
    a !^^ b
    a xnor b
    ---
    const xnor: (a: unknown, b: unknown) => boolean = (a, b) => a || b ? a && b : true;
    xnor(a,b)
    xnor(a,b)
    xnor(a, b)
    xnor(a, b)
    xnor(a, b)
  """

  testCase """
    weird spacing
    ---
    a
        + 123
     /*heyy*/    + 1

           + b
    ---
    a
        + 123
     /*heyy*/    + 1

           + b
  """

  testCase """
    snug newline
    ---
    a+
    b
    ---
    a+
    b
  """

  testCase """
    spaced newline
    ---
    a +
    b
    ---
    a +
    b
  """

  testCase """
    throw expression in logical or binary op
    ---
    a or throw new Error
    ---
    a || (()=>{throw new Error})()
  """

  testCase """
    late assignments
    ---
    x = 3 + y = 2 + z = 1
    ---
    x = 3 + (y = 2 + (z = 1))
  """

  testCase """
    instanceof
    ---
    a instanceof b
    ---
    a instanceof b
  """

  testCase """
    not instanceof
    ---
    a not instanceof b
    ---
    !(a instanceof b)
  """

  testCase """
    pipe expression
    ---
    a |> fn
    ---
    fn(a)
  """

  testCase """
    chained pipe expression
    ---
    a |> foo |> bar
    ---
    bar(foo(a))
  """

  testCase """
    pipe expression with shorthand functions
    ---
    a |> & + 1 |> bar
    ---
    bar(($ => $ + 1)(a))
  """

  testCase """
    nested pipelines
    ---
    (a |> & + 1) |> (b |> bar)
    ---
    ((bar(b)))((($ => $ + 1)(a)))
  """

  testCase """
    multi line trailing
    ---
    x + 1 |>
      & * 2 |>
      foo |>
      baz(1)
    ---
    (baz(1))(foo(($ => $ * 2)(x + 1)))
  """

  testCase """
    multi line trailing with comments
    ---
    x + 1 |> // Next step:
      & * 2 |> // Subsequent step:
      foo |> /* Last step: */
      baz(1)
    ---
    ( /* Last step: */
      baz(1))( // Subsequent step:
      foo(( // Next step:
      $ => $ * 2)(x + 1)))
  """

  testCase """
    multi line leading
    ---
    x + 1
    |> & * 2
    |> foo
    |> baz(1)
    ---
    (baz(1))(foo(($ => $ * 2)(x + 1)))
  """

  testCase """
    multi line leading with comments
    ---
    x + 1
    |> & * 2 // Adds two
    |> foo // Processes through foo
    |> baz(1)
    ---
    (baz(1))(foo // Processes through foo
    (($ => $ * 2 // Adds two
    )(x + 1)))
  """

  testCase """
    await in pipeline
    ---
    x + 1
    |> performAsyncOp
    |> await
    |> foo
    ---
    foo(await performAsyncOp(x + 1))
  """

  testCase """
    yield in pipeline
    ---
    x + 1
    |> yield
    |> foo
    ---
    foo(yield x + 1)
  """

  testCase """
    return in pipeline
    ---
    x + 1
    |> foo
    |> return
    ---
    return foo(x + 1)
  """

describe "custom identifier infix operators", ->
  testCase """
    bless
    ---
    operator foo
    a foo b
    a bar b
    ---

    foo(a, b)
    a(bar(b))
  """

  testCase """
    chain
    ---
    operator foo
    a foo b foo c
    ---

    foo(foo(a, b), c)
  """

  testCase """
    use operator as leading function
    ---
    operator foo
    foo a, b
    ---

    foo(a, b)
  """

  testCase """
    use operator as function
    ---
    operator foo
    a foo foo(b, c)
    ---

    foo(a, foo(b, c))
  """

  testCase """
    multiple bless
    ---
    operator foo, bar
    a foo b bar c
    ---

    bar(foo(a, b), c)
  """

  testCase """
    function operator
    ---
    operator min(a, b)
      Math.min a, b
    a min b
    ---
    function min(a, b) {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    operator function operator
    ---
    operator function min(a, b)
      Math.min a, b
    a min b
    ---
    function min(a, b) {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    function operator with types
    ---
    operator min(a: number, b: number): number
      Math.min a, b
    a min b
    ---
    function min(a: number, b: number): number {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    function operator with braces
    ---
    operator min(a, b) {
      Math.min a, b
    }
    a min b
    ---
    function min(a, b) {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    export function operator
    ---
    export operator min(a, b)
      Math.min a, b
    a min b
    ---
    export function min(a, b) {
      return Math.min(a, b)
    }
    min(a, b)
  """

  testCase """
    let operator
    ---
    operator let min = Math.min, max = Math.max
    a min b max c
    ---
    let min = Math.min, max = Math.max
    max(min(a, b), c)
  """

  testCase """
    const operator
    ---
    operator const {min, max} = Math
    a min b max c
    ---
    const {min, max} = Math
    max(min(a, b), c)
  """

  testCase """
    const shorthand operator
    ---
    operator {min, max} := Math
    a min b max c
    ---
    const {min, max} = Math
    max(min(a, b), c)
  """

  testCase """
    import operator
    ---
    import { operator min, max } from 'ops'
    a min b max c
    ---
    import { min, max } from 'ops'
    min(a, b(max(c)))
  """

  testCase """
    import operators
    ---
    import { operator min, operator max } from 'ops'
    a min b max c
    ---
    import { min, max } from 'ops'
    max(min(a, b), c)
  """

  testCase """
    operator in parens acts as function
    ---
    operator foo
    x (foo) y
    ---

    x((foo)(y))
  """

  testCase """
    not operator
    ---
    operator foo
    x not foo y
    ---

    !foo(x, y)
  """
