{ compile } from ../source/main.coffee

assert from assert
cache := true

compare := (src: string, result: string, compilerOpts: any) ->
  { filename } := compilerOpts
  compileResult := compile(src, {
    noCache: !cache
    ...compilerOpts
  })

  assert.equal compileResult, result, """
    #{filename}
    --- Source   ---
    #{src}

    --- Expected ---
    #{result}

    --- Got      ---
    #{compileResult}

  """

testCase := (text: string, opt?: "only" | "skip", compilerOpts: any={}) ->
  [desc, src, result] := text.split("\n---\n")

  fn := opt ? it[opt] : it

  fn desc, ->
    compare src, result, {
      filename: desc
      ...compilerOpts
    }

testCase.only = (text: string) -> testCase text, "only"
testCase.skip = (text: string) -> testCase text, "skip"

testCase.js = (text: string) ->
  testCase(text, undefined, {
    js: true
  })

throws := (text: string, opt?: "only" | "skip") ->
  let [desc, src] = text.split("\n---\n")

  let fn
  if src?
    fn = opt ? it[opt] : it
  else
    // Backward compatibility mode: just code, no description
    src = desc
    fn = (_desc: string, x: () => void) => x()

  fn desc, ->
    let e: unknown, result: string
    try
      result = compile src, {
        cache
      }
    catch caught
      e = caught
    assert.throws => e && throw e, (undefined as any), """

      --- Source   ---
      #{src}

      --- Got      ---
      #{result!}

    """

throws.only = (text: string) -> testCase text, "only"
throws.skip = (text: string) -> testCase text, "skip"

evalsTo := (src: string, value: any) ->
  result := eval compile src, {
    cache
    js: true
  }
  assert.equal result, value, """

    --- Source   ---
    #{src}

    --- Expected ---
    #{value}

    --- Got      ---
    #{result}

  """

export {
  compare
  evalsTo
  testCase
  throws
}
