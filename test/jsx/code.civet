{ testCase } from ../helper.civet

describe "jsxCode", ->
  testCase """
    nested
    ---
    "civet jsxCodeNested"
    Component := (items: Item[]) =>
      <ul>
        for item of items
          <li>
            if item.type === "link"
              <a href=item.url>some link title: {item.title}
            else
              <p>{item.content}
    ---
    const Component = (items: Item[]) => {
      return <ul>
        {(()=>{const results=[];for (const item of items) {
          results.push(<li>
            {(item.type === "link"?
              <a href={item.url}>some link title: {item.title}
              </a>
            :
              <p>{item.content}
              </p>)}
          </li>)
        }return results})()}
      </ul>
    }
  """

  testCase """
    same line
    ---
    "civet jsxCodeSameLine"
    <div>
      Logged in as:
      <span .name> name
    </div>
    ---
    <div>
      Logged in as:
      <span class="name">{name}
      </span>
    </div>
  """

  testCase """
    both
    ---
    "civet jsxCode"
    <div>
      if items
        <h1> "List of items:"
        <ul> for item in items
          <li> <span> item
      else
        <span> "No items found"
    ---
    <div>
      {(items?
        <>
        <h1>{"List of items:"}
        </h1>
        <ul>{(()=>{const results=[];for (const item in items) {
          results.push(<li><span>{item}
          </span>
          </li>)
        }return results})()}
        </ul>
        </>
      :
        <span>{"No items found"}
        </span>)}
    </div>
  """

  testCase """
    declaration
    ---
    "civet jsxCode"
    <div>
      user := getUser()
      if user?
        {name} := user
        <h1> `Welcome ${name}!`
      <h2>Posts</h2>
      for post of posts
        <div .post> post.jsx()
    ---
    let user;<div>
      {user = getUser(),void 0}
      {(()=>{if (user != null) {
        const {name} = user
        return <h1>{`Welcome ${name}!`}
        </h1>
      };return})()}
      <h2>{Posts}</h2>
      {(()=>{const results=[];for (const post of posts) {
        results.push(<div class="post">{post.jsx()}
        </div>)
      }return results})()}
    </div>
  """

  testCase """
    this declaration
    ---
    "civet jsxCode"
    <div>
      {@first, @last} := getUser()
    ---
    let first,last;<div>
      {{first, last} = getUser(),this.first = first,this.last = last,void 0}
    </div>
  """

  testCase """
    splice declaration
    ---
    "civet jsxCode"
    <div>
      [first, ...middle, last] := getUser()
    ---
    let first,last,middle;<div>
      {[first, ...middle] = getUser(), [last] = middle.splice(-1),void 0}
    </div>
  """
