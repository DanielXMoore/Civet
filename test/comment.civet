{testCase, throws} from ./helper.civet

describe "comment", ->
  testCase """
    keeps comments as written
    ---
    // Hi
    ---
    // Hi
  """

  testCase """
    keeps comments at beginning and end of file
    ---
    // Hi
    x
    /* bye */
    ---
    // Hi
    x
    /* bye */
  """

  testCase """
    CoffeeScript style multi-line comments
    ---
    ###
    hi
    ###

    ---
    /*
    hi
    */

  """

  it "doesn't allow */ in ### block comments", ->
    throws """
      ###
      hi */
      ###
    """

  testCase """
    remains after empty statement in nested block
    ---
    ->
      ; // Hey
    ---
    function() {
      ; // Hey
    }
  """

  testCase """
    inline stays in empty statement
    ---
    ->
      /**/; // Hey
    ---
    function() {
      /**/; // Hey
    }
  """

  testCase """
    space before trailing comment doesn't cause application
    ---
    x := 6 // Hey
    y
    ---
    const x = 6 // Hey
    y
  """

  testCase """
    coffee comment with "civet coffee-compat"
    ---
    "civet coffee-compat"
    # coffeescript: 1
    ---
    // coffeescript: 1
  """

  testCase '''
    coffee multiline comment with coffee single line comments
    ---
    "civet coffee-compat"
    #
    ###
    .
    ###
    ---
    //
    /*
    .
    */
  '''

  testCase '''
    quad octothorpe single line comment
    ---
    "civet coffee-compat"
    #### hi
    x
    ---
    //### hi
    x
  '''
