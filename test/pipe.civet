{ testCase, throws } from ./helper.civet

describe "pipe", ->
  testCase """
    pipe expression
    ---
    a |> fn
    ---
    fn(a)
  """

  testCase """
    chained pipe expression
    ---
    a |> foo |> bar
    ---
    bar(foo(a))
  """

  testCase """
    pipe expression with shorthand functions
    ---
    a |> & + 1 |> bar
    ---
    bar(a + 1)
  """

  testCase """
    nested pipelines
    ---
    (a |> & + 1) |> (b |> bar)
    ---
    (bar(b))((a + 1))
  """

  testCase """
    multi line trailing
    ---
    x + 1 |>
      & * 2 |>
      foo |>
      baz(1)
    ---
    baz(1)(foo((x + 1) * 2))
  """

  testCase """
    multi line trailing with comments
    ---
    x + 1 |> // Next step:
      & * 2 |> // Subsequent step:
      foo |> /* Last step: */
      baz(1)
    ---
    baz(1) /* Last step: */
      (foo // Subsequent step:
      ((x + 1) * 2 // Next step:
      ))
  """

  testCase """
    multi line leading
    ---
    x + 1
    |> & * 2
    |> foo
    |> baz(1)
    ---
    baz(1)(foo((x + 1) * 2))
  """

  testCase """
    multi line leading with comments
    ---
    x + 1
    |> & * 2 // Adds two
    |> foo // Processes through foo
    |> baz(1)
    ---
    baz // Processes through foo
    (1)(foo // Adds two
    ((x + 1) * 2))
    """

  testCase """
    await in pipeline
    ---
    x + 1
    |> performAsyncOp
    |> await
    |> foo
    ---
    foo(await performAsyncOp(x + 1))
  """

  testCase """
    yield in pipeline
    ---
    x + 1
    |> yield
    |> foo
    ---
    foo(yield x + 1)
  """

  testCase """
    return in pipeline
    ---
    x + 1
    |> foo
    |> return
    ---
    return foo(x + 1)
  """

  testCase """
    return pipeline expression
    ---
    return a |> fn
    ---
    return fn(a)
  """

  // TODO: reduce excess parens
  testCase """
    no nested pipelines in implicit arguments
    ---
    array |> .sort ((x, y) => x - y) |> .join ',' |> console.log
    ---
    console.log((array.sort(((x, y) => x - y))).join(','))
  """

  testCase """
    nested pipelines within nested arguments
    ---
    func |> .call @,
      arg1 |> op1 |> op2
      arg2 |> op3 |> op4
    ---
    func.call(this,
      op2(op1(arg1)),
      op4(op3(arg2)),)
  """

  throws """
    pipe after return is error
    ---
    a |> b |> return |> c
  """

  describe "thicc", ->
    testCase """
      basic
      ---
      x ||> f
      ---
      f(x),x
    """

    testCase """
      thick pipe first
      ---
      x ||> f |> g
      ---
      f(x),g(x)
    """

    // TODO: ref to avoid duplicate evaluation of f(x)
    // let ref
    // g(ref=f(x)),ref
    testCase """
      thick pipe second
      ---
      x
      |> f
      ||> g
      ---
      g(f(x)),f(x)
    """

    testCase """
      return
      ---
      count |> &+1
      ||> (c) => console.log c
      |> return
      ---
      ((c) => console.log(c))(count+1);return count+1
    """

    testCase """
      complex return
      ---
      fetch url |> await
      ||> ({status}) =>
            console.log `Fetched ${url} with ${status}`
      |> .json() |> await
      ||> (json) =>
            console.log `Parsed JSON ${json}`
      |> return
      ---
      (({status}) => {
            return console.log(`Fetched ${url} with ${status}`)
      })(await fetch(url)),((json) => {
            return console.log(`Parsed JSON ${json}`)
      })(await (await fetch(url)).json());return await (await fetch(url)).json()
    """

    testCase """
      two thick pipes
      ---
      x
      ||> f
      ||> g
      ---
      f(x),g(x),x
    """

    // TODO: ref for document.title
    testCase """
      accessignment
      ---
      document.title |>= &.toUpperCase()
      ---
      document.title = document.title.toUpperCase()
    """

    testCase """
      array operations with explicit calls
      ---
      array ||> .push(3) ||> .shift() ||> .sort()
      ---
      array.push(3),array.shift(),array.sort(),array
    """

    testCase """
      array operations with implicit calls
      ---
      array ||> .push 3 ||> .shift() ||> .sort()
      ---
      array.push(3),array.shift(),array.sort(),array
    """

    testCase """
      multi-line array operations with implicit calls
      ---
      array
      ||> .push 3
      ||> .shift()
      ||> .sort()
      ---
      array.push(3),array.shift(),array.sort(),array
    """

    testCase """
      pipe assignment
      ---
      lhs |>= func1 |> func2 |> func3 |> func4
      ---
      lhs = func4(func3(func2(func1(lhs))))
    """

  describe "assignment outside of first position", ->
    throws """
      pipe then assign
      ---
      a |> b |>= c
    """

    throws """
      multiple assigns
      ---
      a |>= b |>= c |>= d |>= e
    """
