/**
@file esbuild plugin for Civet language

@example
```javascript
import esbuild from 'esbuild'
import civetPlugin from '@danielx/civet/esbuild-plugin'

esbuild.build({
  ...,
  plugins: [
    civetPlugin
  ]
}).catch(() => process.exit(1))
```
*/

{ readFile } := require 'fs/promises'
path := require 'path'

// NOTE: this references the built version of the module, not the source
{ compile } := require "../dist/main.js"

module.exports = {
  name: 'civet'
  setup(build)
    build.onLoad {
      filter: /\.civet$/
    }, (args) ->
      try
        source := await readFile args.path, 'utf8'
        filename := path.relative(process.cwd(), args.path)
        compiled := compile source, {
          filename
          inlineMap: true
          js: true
        }

        return
          contents: compiled
      catch e
        return
          errors: [{
            text: e.message
          }]
}
