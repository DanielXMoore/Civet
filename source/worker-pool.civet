type { Worker } from node:worker_threads
{ SourceMap } from './sourcemap.civet'

type Job
  id: number
  op: string
  args: ???[]

export class WorkerPool
  idle: Worker[]
  spawned: number
  jobId: number
  callbacks: Map<number, {job: Job, resolve: (result: ???) =>, reject: (error: ???) =>}>
  todo: Job[]
  @(@threads: number)
    @idle = []
    @spawned = 0
    @jobId = 0
    @callbacks = new Map
    @todo = []

  run(op: string, ...args: ???[]): Promise<???>
    id := @jobId++
    await new Promise (resolve, reject): void =>
      job := {id, op, args}
      @callbacks.set id, {job, resolve, reject}
      if @idle#
        worker := @idle.shift()!
        worker.ref()
        worker.postMessage job
      else if @spawned < @threads
        @startWorker() |> await |> .postMessage job
      else
        @todo.push job

  startWorker(): Promise<Worker>
    @spawned++
    { Worker } from node:worker_threads
    path from node:path
    :ESM_ONLY globalThis.__dirname = do
      { fileURLToPath } from node:url
      path.dirname fileURLToPath import.meta.url
    worker := new Worker path.join __dirname, 'node-worker.mjs'
    worker.on 'message', (response: {id: number, errors?: unknown[]?, result?: ???, error?: ???}) =>
      callback := @callbacks.get(response.id)!
      @callbacks.delete response.id
      if response.error
        // Give worker time to output its error message
        await new Promise (done) => setTimeout done, 0
        message := `${response.error.name}: ${response.error.message}`
        error :=
          if response.error.type in globalThis
            new globalThis[response.error.type] message
          else
            new Error message
        try error.name = response.error.name
        callback.reject error
      else
        if sourceMap := response.result?.sourceMap
          // Restore SourceMap class wrapper not preserved by worker message
          response.result.sourceMap = new SourceMap sourceMap.source
          Object.assign response.result.sourceMap, sourceMap
        if errors := callback.job.args[1]?.errors
          // Restore errors array aliasing
          errors[..] = response.errors!
        callback.resolve response.result
      // Worker is now available
      if @spawned > @threads // kill if now too many workers
        @spawned--
        worker.terminate()
      else if @todo# // send job if any already available
        worker.postMessage @todo.shift()
      else // otherwise add to idle set and don't block program exit
        @idle.push worker
        worker.unref()
    worker.on 'error', (error) =>
      console.error "Civet worker failed:", error
    worker

  setThreads(@threads: number): void
    while @spawned > @threads
      return unless @idle#
      @spawned--
      @idle.pop()!.terminate()
