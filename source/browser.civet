export * from ./main.civet
import { type CompilerOptions, compile } from ./main.civet

// Based on `CoffeeScript.runScripts`, from
// https://github.com/jashkenas/coffeescript/blob/main/src/browser.coffee
export function runScripts(type = 'text/civet'): void
  scripts := window.document.querySelectorAll `script[type=${JSON.stringify type}]`

  for each script, i of scripts
    options: CompilerOptions :=
      inlineMap: true
      js: true

    let code: string
    if source := (script as HTMLScriptElement).src or script.getAttribute 'data-src'
      options.filename = source
      code = fetch source |> await |> .text() |> await
    else
      options.filename = script.id or `<script>${i}`
      code = script.innerHTML

    js := compile code, options |> await
    window.eval js

// Check for <script type="text/civet"> automatically in browser context
// (not e.g. worker thread)
unless window <? 'undefined'
  window?.addEventListener? 'DOMContentLoaded', => runScripts()
