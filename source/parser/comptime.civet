"civet coffeePrototype"

// These get replaced by shims from browser.civet on the browser
{ resolve, dirname } from "node:path"
{ createRequire } from "node:module"
vm from "node:vm"

import type {
  ComptimeExpression
  ComptimeStatement
  StatementTuple
} from ./types.civet

import {
  gatherRecursive
} from "./traversal.civet"

{ extractPreludeFor } from "./helper.civet"
{ getFilename, getInitialConfig, getSync } from "../parser.hera"

generate, { prune, type Options } from "../generate.civet"

function processComptime(statements: StatementTuple[]): void | Promise<void>
  // Prevent comptime setting from being overridden by directives
  // by looking at initialConfig instead of config
  return unless getInitialConfig()?.comptime

  promises := runComptime statements
  if getSync()
    return
  else
    async do
      await.all promises
      ;

// Launch comptime evaluations, and return promises to await (in async mode)
function runComptime(statements: StatementTuple[]): unknown[]
  sync := getSync()
  gatherRecursive statements,
    (node): node is (ComptimeStatement | ComptimeExpression) =>
      node.type is "ComptimeStatement" or node.type is "ComptimeExpression"
  .map (exp) =>
    content .= exp.type is "ComptimeStatement" ? exp.block : exp.expression

    // Copy used helpers
    content = [
      ...extractPreludeFor content
      content
    ]

    // Convert the comptime block into JS code
    options: Options := js: true
    js .= generate prune(content), options
    js = `"use strict";${js}`
    // If there are any errors, leave the comptime subtree alone
    // (so there's still an error).
    return if options.errors?

    // Run the JS code at compile time (now)
    let output: unknown, context: NodeJS.Dict<unknown>, contextGlobal?: NodeJS.Dict<unknown>
    try
      context = vm.createContext?() ?? globalThis
      filename := context.__filename = resolve getFilename() ?? ""
      context.__dirname = dirname filename
      context.require = createRequire filename
      if vm.runInContext?
        // Copy over Node-specific built-ins to new context, based on
        // https://github.com/nodejs/node/blob/c7e42092f34f019fa0c4d9a2d0d49719af2f5daa/lib/repl.js#L1110
        contextGlobal = vm.runInContext "globalThis", context
        builtins := new Set Object.getOwnPropertyNames contextGlobal
        for name of Object.getOwnPropertyNames globalThis
          continue if builtins.has name
          Object.defineProperty contextGlobal, name, {
            __proto__: null
            ...Object.getOwnPropertyDescriptor globalThis, name
          }
        output = vm.runInContext js, context, {
          filename
          importModuleDynamically:
            vm.constants?.USE_MAIN_CONTEXT_DEFAULT_LOADER
        }
      else
        // Indirect eval call (via ?.) causes evaluation in global scope,
        // instead of the scope of this function.
        // https://developer.mozilla.org/en-US/docs/Web/JavaScript/Reference/Global_Objects/eval#direct_and_indirect_eval
        output = eval?.(js)
    catch e
      exp.children = [
        type: "Error"
        message: `comptime block failed to execute: ${e}\n${js}`
      ]
      return

    // Await result (in async mode only) and serialize output
    let promise: unknown
    if exp.type is "ComptimeExpression"
      finish := =>
        let string
        try
          string = serialize output, contextGlobal
        catch e
          exp.children = [
            type: "Error"
            message: `comptime result ${output} not serializable: ${e}`
          ]
          return
        exp.children = [string]
      if sync
        finish()
      else
        promise = async do
          output = await output
          finish()
    else
      // Statements do not need to have their result inlined,
      // but we still might need to await the output
      promise = output
      exp.children = []
    promise

function serialize(value: ???, context?: NodeJS.Dict<any>): string
  stack := new Set<object>
  function recurse(val: ???): string
    switch val
      <? "string"
        // Using JSON.stringify to handle escape sequences
        JSON.stringify val
      <? "number"
        if Object.is -0, val then '-0' else val.toString()
      <? "boolean", == null
        String val // not toString to accomodate null/undefined
      <? "bigint"
        `${val}n`
      <? "function"
        // Some functions can actually be serialized
        string .= Function::toString.call val
        if /\{\s+\[native code]\s+\}$/.test string
          // builtin, or returned from Function::bind
          throw new TypeError "cannot serialize native function"
        if /^class[\s{]/u.test string
          // Skip other checks; properties on the function are `static` things in the class
          return string

        if stack.has val
          throw new Error "circular reference detected"
        stack.add val

        // Check for ES6 methods that have been removed from the object, and add "function" if possible
        if /^(?:async\s*)?(?:\*\s*|[gs]et\s+)?\[/.test string
          // In general, the method name could be an arbitrary JS expression. This is not currently handled.
          throw new Error "cannot serialize method with computed name"
        // Test for old-style classes done by creating a regular function and assigning to its prototype
        protoHasProps := not (or)
          val:: is undefined
          (and)
            Object:: is Object.getPrototypeOf val::
            Object.getOwnPropertyNames(val::)# <= 1 // constructor
            Object.getOwnPropertySymbols(val::)# is 0
            val::constructor is in [val, undefined]
        isGenerator := /^(?:async\s*)?(?:function\s*)?\*/u.test string
        if protoHasProps and not isGenerator
          throw new TypeError "cannot serialize function with modified prototype"
        unless /^(?:async\s+)?(?:(function|class)(?!\p{ID_Continue})|\(|(?:\p{ID_Start}|[_$])(?:\p{ID_Continue}|[\u200C\u200D$])*\s*=>)/u.test string
          // Slightly more annoying than just `'function ' + string`: need to insert it before `async`, and remove `get` or `set` if applicable
          string |>= .replace /^(async\s+)?(?:[gs]et\s+(?=\p{ID_Start}))?/u, (_, maybeAsync = '') => maybeAsync + 'function '

        // Properties that a function may have inherently even if they aren't explicitly assigned
        defaultProps := ["length", "name", "arguments", "caller", "prototype"]

        hasProps := not (and)
          Object.getOwnPropertyNames(val).every & is in defaultProps
          Object.getOwnPropertySymbols(val)# is 0
        if hasProps
          props := Object.getOwnPropertyDescriptors val
          for prop of defaultProps
            delete props[prop]
          string = `Object.defineProperties(${string},${recurse props})`

        stack.delete val
        string
      <? "symbol"
        if key? := Symbol.keyFor val
          return `Symbol.for(${JSON.stringify key})`
        // Handle well-known symbols
        // for-in doesn't find them, but getOwnPropertyNames does
        for name of Object.getOwnPropertyNames Symbol
          sym := (Symbol as! Record<string, symbol>)[name]
          if val is sym
            return `Symbol.${name}`
        throw new TypeError "cannot serialize unique symbol"
      <? "object"
        if stack.has val
          throw new Error "circular reference detected"
        stack.add val
        str :=
          switch Object.getPrototypeOf val
            when RegExp::, context?.RegExp::
              re := val as RegExp
              `/${re.source}/${re.flags}`
            when Date::, context?.Date::
              `new Date(${(val as Date).getTime()})`
            when Set::, context?.Set::
              "new Set([" + (
                for item of val as Set<???>
                  recurse item
              ).join(",") + "])"
            when Map::, context?.Map::
              "new Map([" + (
                for [key, value] of val as Map<???, ???>
                  `[${recurse key},${recurse value}]`
              ).join(",") + "])"
            when Object::, context?.Object::
              // The string representing the object's own enumerable, writable properties
              objStr .= '{'
              // The string representing other property descriptors, if applicable, not including the outermost braces
              descStr .= ''
              for each prop of (Object.getOwnPropertyNames(val) as (string|symbol)[]) ++ Object.getOwnPropertySymbols(val)
                desc := Object.getOwnPropertyDescriptor(val, prop)!
                if desc.enumerable and desc.configurable and desc.writable
                  if prop <? 'symbol'
                    objStr += `[${recurse prop}]`
                  else
                    objStr += JSON.stringify prop
                  objStr += `:${recurse desc.value},`
                else
                  if prop <? 'symbol'
                    descStr += `[${recurse prop}]`
                  else
                    descStr += JSON.stringify prop
                  descStr += `:${recurse desc},`
              // remove trailing comma
              if objStr# > 1
                objStr = objStr[...-1]
              objStr += '}'
              if descStr !== ''
                objStr = `Object.defineProperties(${objStr},\{${descStr[...-1]}\})`
              if not Object.isExtensible val
                objStr = `Object.preventExtensions(${objStr})`
              objStr
            when URL::, context?.URL::
              `new URL(${JSON.stringify (val as URL).href})`
            when null
              `Object.create(null,${recurse Object.getOwnPropertyDescriptors val})`
            when Int8Array::, Uint8Array::, Int16Array::, Uint16Array::, Int32Array::, Uint32Array::, Float32Array::, Float64Array::, Uint8ClampedArray::, context?.Int8Array::, context?.Uint8Array::, context?.Int16Array::, context?.Uint16Array::, context?.Int32Array::, context?.Uint32Array::, context?.Float32Array::, context?.Float64Array::, context?.Uint8ClampedArray::
              // There's no "TypedArray" interface in TS
              `new ${val.constructor.name}([${(val as any).join ','}])`
            when BigInt64Array::, BigUint64Array::, context?.BigInt64Array::, context?.BigUint64Array::
              `new ${val.constructor.name}([${Array.from(val as ArrayLike<bigint>, `${&}n`).join ','}])`
            // Spelled differently for browsers, where `Buffer` doesn't exist
            when globalThis.Buffer?::, context?.Buffer?::
              `Buffer.from([${(val as Buffer).join ','}])`
            else
              if Array.isArray val
                `[${(val as ???[]).map(recurse).join ","}]`
              else
                // One day we may handle other classes like so:
                // str += `__proto__:${val.constructor.name}`
                throw new TypeError `cannot serialize object with prototype ${val.constructor?.name ?? Object.getPrototypeOf val}`
        stack.delete val
        str
      else
        throw new TypeError `cannot serialize ${typeof val} value`
  recurse value

export {
  processComptime
  serialize
}
