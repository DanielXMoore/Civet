import type {
  ASTNode
  ASTNodeObject
  ASTRef
  ASTString
} from ./types.civet

import {
  gatherRecursive
} from ./traversal.civet

import {
  isWhitespaceOrEmpty
  makeNode
} from ./util.civet

function makeRef(base = "ref", id = base): ASTRef
  return {
    type: "Ref"
    base
    id
  }

/**
 * Returns a new ref if the expression needs a ref (not a simple value).
 * Otherwise returns undefined.
 */
function needsRef(expression: ASTNode, base = "ref"): ASTRef | undefined
  return unless expression? <? "object"
  if Array.isArray expression
    nonempty := [0...expression.length]
    .filter (i) => not isWhitespaceOrEmpty expression[i]
    if nonempty# is 1
      if ref := needsRef expression[nonempty[0]], base
        return ref
      return
    else
      return makeRef base
  switch expression.type
    when "Ref", "Identifier", "Literal", "Placeholder"
      return
  return makeRef(base)

// Transform into a ref if needed
function maybeRef(exp: ASTNode, base: string = "ref"): ASTNode
  needsRef(exp, base) or exp

/**
* Make an AssignmentExpression node that assigns `ref` to `exp`,
* and hoists a `let ref` declaration. Returned in `refAssignment` property,
* along with `refAssignmentComma` which is an array of the assignment
* followed by a comma, for easy splicing into children.
*/
function makeRefAssignment(ref: ASTNode, exp: ASTNode): {
  refAssignment: ASTNodeObject
  refAssignmentComma: ASTNode[]
}
  refAssignment := makeNode
    type: "AssignmentExpression"
    children: [ref, " = ", exp]
    hoistDec:
      type: "Declaration"
      children: ["let ", ref]
      names: []
  {
    refAssignment
    refAssignmentComma: [refAssignment, ","]
  }

/**
* Returns `{ ref, refAssignment, refAssignmentComma }` where
* * `ref` is a ref for `exp` if it's complex, otherwise `exp` itself
* * `refAssignment` is an assignment expression that assigns `ref` to `exp`
*   and hoists a `let ref` declaration, or if there a ref wasn't needed,
*   `undefined` (actually lacking property)
* * `refAssignmentComma` is an array of the assignment followed by a comma,
*   or an empty array if there's no assignment, for easy splicing into children
* Essentially `maybeRef` + `makeRefAssignment`.
*/
function maybeRefAssignment(exp: ASTNode, base: string = "ref"): {
  ref: ASTNode
  refAssignment?: ASTNode
  refAssignmentComma: ASTNode[]
}
  ref := maybeRef exp, base
  if ref is exp
    { ref, refAssignmentComma: [] }
  else
    { ref, ...makeRefAssignment ref, exp }

function populateRefs(statements: ASTNode): void
  refNodes := gatherRecursive statements, .type is "Ref"
  return unless refNodes#

  // Find all ids within nested scopes
  ids := gatherRecursive statements, .type is "Identifier"
  names := new Set ids.flatMap { names } => names or []

  // Populate each ref
  for each ref of refNodes
    continue unless ref.type is "Ref" // skip if already populated
    { base } := ref
    ref.type = "Identifier"

    n .= 0
    name .= base

    // check for name collisions and increment name suffix
    while names.has name
      n++
      name = `${base}${n}`

    names.add name
    ref.children = ref.names = [name as ASTString]

export {
  makeRef
  makeRefAssignment
  maybeRef
  maybeRefAssignment
  needsRef
  populateRefs
}
