import type {
  ArrayBindingPatternContent
  ASTNode
  ASTNodeObject
  ASTRef
  BindingProperty
  BindingRestElement
  BindingRestProperty
  BlockStatement
  Condition
  ContinueStatement
  ElseClause
  Identifier
  ObjectBindingPatternContent
  ParenthesizedExpression
  PatternClause
  PatternExpression
  PinProperty
  StatementTuple
  SwitchStatement
} from ./types.civet

import {
  gatherRecursiveAll
} from ./traversal.civet

import {
  flatJoin
  isExit
  makeLeftHandSideExpression
  makeNode
  replaceNode
  updateParentPointers
} from ./util.civet

import {
  makeRef
  maybeRefAssignment
} from ./ref.civet

import {
  blockWithPrefix
  braceBlock
} from ./block.civet

import {
  gatherSubbindings
  gatherBindingCode
} from ./binding.civet

import {
  processBinaryOpExpression
} from ./op.civet

import {
  getHelperRef
} from ./helper.civet

import {
  ReservedWord
} from ../parser.hera

function processPatternTest(lhs: ASTNode, patterns: PatternExpression[]): ASTNode
  { ref, refAssignmentComma } := maybeRefAssignment lhs, "m"

  conditionExpression := patterns
  .map getPatternConditions ., ref
  .map flatJoin ., " && "
  |> flatJoin ., " || "

  makeLeftHandSideExpression makeNode
    type: "PatternTest"
    children: [
      ...refAssignmentComma
      conditionExpression
    ]

function processPatternMatching(statements: ASTNode): void
  gatherRecursiveAll(statements, .type is "SwitchStatement")
    .forEach (s: SwitchStatement) =>
      { caseBlock } := s
      { clauses } := caseBlock

      // Remove inserted `break;` in `when` clauses that don't need them
      for each c of clauses
        if c.type is "WhenClause" and c.break
          if isExit c.block
            c.children.splice c.children.indexOf(c.break), 1
            c.break = undefined

      errors .= false
      isPattern .= false
      if clauses.some .type is "PatternClause"
        isPattern = true
        for each c of clauses
          // else/default clause is ok
          unless c.type is "PatternClause" or c.type is "DefaultClause"
            errors = true
            c.children.push
              type: "Error"
              message: "Can't mix pattern matching and non-pattern matching clauses"

      return if errors or !isPattern

      { condition } .= s
      if condition.type is "ParenthesizedExpression"
        // Unwrap parenthesized expression
        condition = condition.expression as ParenthesizedExpression

      { ref, refAssignmentComma } .= maybeRefAssignment condition, "m"
      root: ASTNode[] := []
      prev .= root
      let e?: ElseClause

      l := clauses.length
      for each c, i of clauses
        if c.type is "DefaultClause"
          if e?
            replaceNode e.block, c.block, e
          else
            prev.push c.block
          break

        { patterns, block } .= c as PatternClause
        pattern .= patterns[0]

        // TODO: multiple binding patterns

        conditionExpression: ASTNode[] := patterns
        .map getPatternConditions ., ref
        .map flatJoin ., " && "
        |> flatJoin ., " || "

        condition: Condition := makeNode {
          type: "ParenthesizedExpression"
          children: ["(", ...refAssignmentComma, conditionExpression, ")"]
          expression: conditionExpression
        }

        braceBlock block
        block = blockWithPrefix getPatternBlockPrefix(pattern, ref), block

        // Insert else if there are more branches
        if i < l - 1
          expressions: StatementTuple[] := []
          block := makeNode {
            type: "BlockStatement"
            expressions
            children: [expressions]
            bare: true
          } satisfies BlockStatement
          e = makeNode {
            type: "ElseClause"
            block
            children: ["\n", "else ", block]
          } satisfies ElseClause
        else
          e = undefined

        prev.push ["", makeNode {
          type: "IfStatement"
          children: ["if", condition, block, e]
          then: block
          else: e
        }]
        refAssignmentComma = []
        prev = e.block.expressions if e?

      s.type = "PatternMatchingStatement"
      s.children = root
      updateParentPointers s

  gatherRecursiveAll(statements,
    (s: ASTNodeObject): s is ContinueStatement =>
      s.type is "ContinueStatement" and s.special is "switch"
  ).forEach (cont) =>
    // `continue switch` is an empty statement,
    // which is appropriate only at the end of a when clause
    // (where it ate a `break` thanks to `isExit`)
    unless cont.parent is like {
      type: "BlockStatement"
      parent: {type: 'WhenClause'}
      expressions: [..., [, ^cont, ...]]
    }
      cont.children.push
        type: "Error"
        message: `'continue switch' can only be used at end of 'when' clause`

function getPatternConditions(
  pattern: PatternExpression,
  ref: ASTNode,
  conditions: ASTNode[] = [],
): ASTNode[]
  if (pattern.rest) return conditions // No conditions for rest elements

  switch pattern.type
    when "ArrayBindingPattern"
      const { elements, length } = pattern,
        hasRest = elements.some((e) => e.rest),
        l = (length - +hasRest).toString(),
        lengthCheck = hasRest
        ? [ref, ".length >= ", l]
        : [getHelperRef("len"), "(", ref, ", ", l, ")"]

      conditions.push(
        ["Array.isArray(", ref, ")"],
        lengthCheck,
      )

      // recursively collect nested conditions
      elements.forEach(({ children: [, e] }, i) => {
        const subRef = [ref, "[", i.toString(), "]"]
        getPatternConditions(e, subRef, conditions)
      })

      // collect post rest conditions
      { blockPrefix } := pattern
      if blockPrefix
        postElements := blockPrefix.children[1]
        { length: postLength } := postElements

        postElements.forEach(({ children: [, e] }, i) => {
          const subRef = [ref, "[", ref, ".length - ", (postLength + i).toString(), "]"]
          getPatternConditions(e, subRef, conditions)
        })

    when "ObjectBindingPattern"
      conditions.push
        ["typeof ", ref, " === 'object'"]
        [ref, " != null"]

      for each p of pattern.properties
        switch p.type
          when "PinProperty", "BindingProperty"
            { name, value } := p
            let subRef
            switch name.type
              when "ComputedPropertyName"
                conditions.push([name.expression, " in ", ref])
                subRef = [ref, name]
              when "Literal", "StringLiteral", "NumericLiteral"
                conditions.push([name, " in ", ref])
                subRef = [ref, "[", name, "]"]
              when "Identifier"
                conditions.push(["'", name.name, "' in ", ref])
                subRef = [ref, ".", name.name]
              when "AtBinding"
                conditions.push(["'", name.ref.id, "' in ", ref])
                subRef = [ref, ".", name.ref.id]

            getPatternConditions(value, subRef, conditions) if value

    when "ConditionFragment"
      { rhs } .= pattern
      // Add leading space to first binary operation
      if rhs#
        [ first, ...rest ] .= rhs
        [ ws, ...op ] .= first
        ws = [" "].concat ws
        first = [ ws, ...op ]
        rhs = [ first, ...rest ]
      conditions.push processBinaryOpExpression [ref, rhs]

    when "RegularExpressionLiteral"
      conditions.push(
        ["typeof ", ref, " === 'string'"],
        [pattern, ".test(", ref, ")"],
      )

    when "PinPattern"
      conditions.push([
        ref,
        " === ",
        pattern.expression,
      ])

    when "NamedBindingPattern"
      getPatternConditions pattern.pattern, ref, conditions

    when "Literal"
      conditions.push([
        ref,
        " === ",
        pattern,
      ])

  return conditions

function getPatternBlockPrefix(
  pattern: PatternExpression
  ref: ASTNode
  decl: ASTNode = "const "
  typeSuffix?: ASTNode
): StatementTuple[]?
  switch pattern.type
    when "ArrayBindingPattern"
      return unless pattern.length
    when "ObjectBindingPattern"
      return unless pattern.properties.length
    when "Literal", "RegularExpressionLiteral", "PinPattern"
      return
    when "ConditionFragment"
      return unless pattern.binding

  // Gather bindings
  [splices, thisAssignments] .= gatherBindingCode(pattern)
  patternBindings := nonMatcherBindings(pattern)
  subbindings := gatherSubbindings patternBindings

  splices = splices.map (s) => [", ", nonMatcherBindings(s)]
  thisAssignments = thisAssignments.map ['', &, ";"]

  duplicateDeclarations := aggregateDuplicateBindings([patternBindings, splices])

  [
    ['', {
      type: "Declaration"
      children: [decl, patternBindings, typeSuffix, " = ", ref, ...subbindings, ...splices]
      names: []
      bindings: [] // avoid implicit return of any bindings
    }, ";"]
    ...thisAssignments
    ...duplicateDeclarations.map ['', &, ";"]
  ]

function elideMatchersFromArrayBindings(elements: ArrayBindingPatternContent): ArrayBindingPatternContent
  for each element of elements
    switch element.type
      when "BindingRestElement", "ElisionElement"
        element
      when "BindingElement"
        switch element.binding.type
          when "Literal", "RegularExpressionLiteral", "StringLiteral", "PinPattern"
            element.delim
          else
            binding := nonMatcherBindings element.binding
            makeNode {
              ...element
              binding
              children: element.children.map (c) =>
                c is element.binding ? binding : c
            }

function elideMatchersFromPropertyBindings(properties: ObjectBindingPatternContent): ObjectBindingPatternContent
  for each p of properties
    switch p.type
      when "BindingProperty", "PinProperty"
        { children, name, value } := p
        [ws] := children

        shouldElide := (or)
          name.type is "NumericLiteral" and !value?.name
          name.type is "ComputedPropertyName" and value?.subtype is "NumericLiteral"
        if shouldElide
          continue
        else

          let contents: (BindingProperty | PinProperty)?
          switch value?.type
            when "ArrayBindingPattern", "ObjectBindingPattern"
              bindings := nonMatcherBindings value
              contents = {
                ...p
                value: bindings
                children: [ws, name, bindings && ": ", bindings, p.delim]
              }
            when "Identifier", undefined
              contents = p
            when "NamedBindingPattern"
              bindings := nonMatcherBindings value.pattern
              contents = {
                ...p
                subbinding: if bindings.type is like "ArrayBindingPattern", "ObjectBindingPattern", "Identifier"
                  . bindings
                  . " = "
                  . name
              }
              // Similar to `simplifyBindingProperties`,
              // simplify `{name: name}` to just `{name}`
              // This occurs because the parser expands `{name^: pattern}`
              // into `{name: name^pattern}`
              if p.name is value.binding
                contents!.children = [ws, name, p.delim]
            else // "Literal", "RegularExpressionLiteral", "StringLiteral"
              contents = undefined
          if contents
            contents
          else
            continue
      else // "BindingRestProperty"
        p

function nonMatcherBindings(pattern: ASTNodeObject): ASTNodeObject
  switch pattern.type
    when "ArrayBindingPattern", "PostRestBindingElements"
      elements := elideMatchersFromArrayBindings pattern.elements
      makeNode {
        ...pattern
        elements
        children: pattern.children.map & is pattern.elements ? elements : &
      }
    when "ObjectBindingPattern"
      properties := elideMatchersFromPropertyBindings pattern.properties
      makeNode {
        ...pattern
        properties
        children: pattern.children.map & is pattern.properties ? properties : &
      }
    when "NamedBindingPattern"
      makeNode {
        ...pattern
        subbinding:
          . nonMatcherBindings pattern.pattern
          . " = "
          . pattern.binding
      }
    when "ConditionFragment"
      pattern.binding
    else
      pattern

function aggregateDuplicateBindings(bindings)
  props := gatherRecursiveAll bindings,
    ($): $ is BindingProperty | BindingRestProperty | Identifier | BindingRestElement => (or)
      $.type is "BindingProperty"
      // Don't deduplicate ...rest properties; user should do so manually
      // because ...rest can be named arbitrarily
      //$.type is "BindingRestProperty"
      $.type is "Identifier" and $.parent?.type is "BindingElement"
      $.type is "BindingRestElement"

  declarations: ASTNode[] := []

  propsGroupedByName := new Map<string, ASTNodeObject[]>

  for each p of props
    { name, value } := p

    // We do not bind a property that gets matched against
    // an array or object pattern
    if value?.type is like "ArrayBindingPattern", "ObjectBindingPattern"
      continue

    // This is to handle aliased props, non-aliased props, and binding identifiers in arrays
    const key = value?.name or name?.name or name
    if key?.type is "NumericLiteral" or key?.type is "ComputedPropertyName"
      continue

    if propsGroupedByName.has key
      propsGroupedByName.get(key)!.push(p)
    else
      propsGroupedByName.set key, [p]

  propsGroupedByName.forEach (shared, key) =>
    return unless key
    // NOTE: Allows pattern matching reserved word keys by binding to inaccessible refs
    // HACK: using the parser's ReservedWord rule here
    if ReservedWord({ fail() { } }, {
      pos: 0,
      input: key,
    })
      for each p of shared
        aliasBinding p, makeRef(`_${key}`, key)
      // Don't push declarations for reserved words
      return

    return if shared.length is 1

    // Create a ref alias for each duplicate binding
    refs := shared.map (p) =>
      ref := makeRef(key)
      aliasBinding(p, ref)
      return ref

    // Gather duplicates in an array
    declarations.push
      type: "Declaration"
      children: [
        "const ", key, " = [",
        ...refs.map (r, i) => i is 0 ? r : [", ", r]
        "]"
      ]
      names: []
      bindings: [] // avoid implicit return of any bindings

  return declarations

/**
 * Adjust the alias of a binding property, adding an alias if one doesn't exist or
 * replacing an existing alias. This mutates the property in place.
 */
function aliasBinding(p: ASTNodeObject, ref: ASTRef): void
  if p.type is "Identifier"
    // Array element binding
    // TODO: This ignores `name` and `names` properties of Identifier and
    // hackily converts it to a container for a Ref.
    p.children[0] = ref
  else if p.type is "BindingRestElement"
    aliasBinding p.binding, ref
  else if p.value?.type is "Identifier"
    // aliased property binding
    aliasBinding p.value, ref
  else
    // non-aliased property binding
    p.value = ref
    index := p.children.indexOf p.name
    p.children.splice index + 1, 0, ": ", ref

export {
  getPatternConditions
  getPatternBlockPrefix
  nonMatcherBindings
  processPatternMatching
  processPatternTest
}
